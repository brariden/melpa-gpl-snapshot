* Shell Mode
** Set the path environment variable
#+begin_src emacs-lisp
(setenv "PATH" (concat (getenv "HOME") "/bin:"
                       "/usr/local/bin:"
                       (getenv "PATH")))

(add-to-list 'exec-path "/usr/local/bin")
#+end_src
** Standard Shells
*** Ansi Color On shells
  Ansi color in shell mode is good.

#+name: ansi-color
#+begin_src emacs-lisp
  (autoload 'ansi-color-for-comint-mode-on "ansi-color" nil t)
  (add-hook 'shell-mode-hook 'ansi-color-for-comint-mode-on)
  
#+end_src

*** Set up pcomplete and auto complete on shell
   
   Thanks http://blog.supermomonga.com/!  

   There should be a Japanese/English emacs exchange program.

#+begin_src emacs-lisp
  (defun set-up-auto-complete-for-shell-mode ()
    (setq ac-sources
          '(ac-source-pcomplete
            ac-source-words-in-buffer
            ac-source-dictionary
            )))

(add-hook 'shell-mode-hook 'set-up-auto-complete-for-shell-mode)  
#+end_src
   
**** Git pcompletion in the shell. 
	(From Mikey in Mastering emacs: http://www.masteringemacs.org/articles/2012/01/16/pcomplete-context-sensitive-completion-emacs/)

	Do check out that link to understand how pcomplete works.

#+begin_src emacs-lisp 
(defconst pcmpl-git-commands
  '("add" "bisect" "branch" "checkout" "clone"
    "commit" "diff" "fetch" "flow" "grep"         ; What no git flow?  MADNESS!
    "init" "log" "merge" "mv" "pull" "push" "rebase"
    "reset" "rm" "show" "status" "tag" )
  "List of `git' commands")
 
(defvar pcmpl-git-ref-list-cmd "git for-each-ref refs/ --format='%(refname)'"
  "The `git' command to run to get a list of refs")
 
(defun pcmpl-git-get-refs (type)
  "Return a list of `git' refs filtered by TYPE"
  (with-temp-buffer
    (insert (shell-command-to-string pcmpl-git-ref-list-cmd))
    (goto-char (point-min))
    (let ((ref-list))
      (while (re-search-forward (concat "^refs/" type "/\\(.+\\)$") nil t)
        (add-to-list 'ref-list (match-string 1)))
      ref-list)))
 
(defun pcomplete/git ()
  "Completion for `git'"
  ;; Completion for the command argument.
  (pcomplete-here* pcmpl-git-commands)  
  ;; complete files/dirs forever if the command is `add' or `rm'
  (cond
   ((pcomplete-match (regexp-opt '("add" "rm")) 1)
    (while (pcomplete-here (pcomplete-entries))))
   ;; provide branch completion for the command `checkout'.
   ((pcomplete-match "checkout" 1)
    (pcomplete-here* (pcmpl-git-get-refs "heads")))))
#+end_src

*** Run a shell immediately in emacs so it is available

#+name: shell-on-startup
#+begin_src emacs-lisp
;(add-hook 'emacs-startup-hook 'shell)
#+end_src

** Eshell
#+begin_src emacs-lisp
(add-hook 'emacs-startup-hook 'eshell)
(emagician-expect-package 'eshell-manual)
#+end_src

*** P complete with eshell too.
	Thanks to Tassilo[fn:2] for this bit of wisdom. 

#+begin_src emacs-lisp
;;**** Git Completion

(defun pcmpl-git-commands ()
  "Return the most common git commands by parsing the git output."
  (with-temp-buffer
    (call-process-shell-command "git" nil (current-buffer) nil "help" "--all")
    (goto-char 0)
    (search-forward "available git commands in")
    (let (commands)
      (while (re-search-forward
	      "^[[:blank:]]+\\([[:word:]-.]+\\)[[:blank:]]*\\([[:word:]-.]+\\)?"
	      nil t)
	(push (match-string 1) commands)
	(when (match-string 2)
	  (push (match-string 2) commands)))
      (sort commands #'string<))))

(defconst pcmpl-git-commands (pcmpl-git-commands)
  "List of `git' commands.")

(defvar pcmpl-git-ref-list-cmd "git for-each-ref refs/ --format='%(refname)'"
  "The `git' command to run to get a list of refs.")

(defun pcmpl-git-get-refs (type)
  "Return a list of `git' refs filtered by TYPE."
  (with-temp-buffer
    (insert (shell-command-to-string pcmpl-git-ref-list-cmd))
    (goto-char (point-min))
    (let (refs)
      (while (re-search-forward (concat "^refs/" type "/\\(.+\\)$") nil t)
	(push (match-string 1) refs))
      (nreverse refs))))

(defun pcmpl-git-remotes ()
  "Return a list of remote repositories."
  (split-string (shell-command-to-string "git remote")))

(defun pcomplete/git ()
  "Completion for `git'."
  ;; Completion for the command argument.
  (pcomplete-here* pcmpl-git-commands)
  (cond
   ((pcomplete-match "help" 1)
    (pcomplete-here* pcmpl-git-commands))
   ((pcomplete-match (regexp-opt '("pull" "push")) 1)
    (pcomplete-here (pcmpl-git-remotes)))
   ;; provide branch completion for the command `checkout'.
   ((pcomplete-match "checkout" 1)
    (pcomplete-here* (append (pcmpl-git-get-refs "heads")
			     (pcmpl-git-get-refs "tags"))))
   (t
    (while (pcomplete-here (pcomplete-entries))))))
#+end_src

I only use git currentlky, so the stuff below is useless to me.
#+begin_src emacs-lisp :tangle no
;;**** Bzr Completion

(defun pcmpl-bzr-commands ()
  "Return the most common bzr commands by parsing the bzr output."
  (with-temp-buffer
    (call-process-shell-command "bzr" nil (current-buffer) nil "help" "commands")
    (goto-char 0)
    (let (commands)
      (while (re-search-forward "^\\([[:word:]-]+\\)[[:blank:]]+" nil t)
	(push (match-string 1) commands))
      (sort commands #'string<))))

(defconst pcmpl-bzr-commands (pcmpl-bzr-commands)
  "List of `bzr' commands.")

(defun pcomplete/bzr ()
  "Completion for `bzr'."
  ;; Completion for the command argument.
  (pcomplete-here* pcmpl-bzr-commands)
  (cond
   ((pcomplete-match "help" 1)
    (pcomplete-here* pcmpl-bzr-commands))
   (t
    (while (pcomplete-here (pcomplete-entries))))))

;;**** Mercurial (hg) Completion

(defun pcmpl-hg-commands ()
  "Return the most common hg commands by parsing the hg output."
  (with-temp-buffer
    (call-process-shell-command "hg" nil (current-buffer) nil "-v" "help")
    (goto-char 0)
    (search-forward "list of commands:")
    (let (commands
	  (bound (save-excursion
		   (re-search-forward "^[[:alpha:]]")
		   (forward-line 0)
		   (point))))
      (while (re-search-forward
	      "^[[:blank:]]\\([[:word:]]+\\(?:, [[:word:]]+\\)*\\)" bound t)
	(let ((match (match-string 1)))
	  (if (not (string-match "," match))
	      (push (match-string 1) commands)
	    (dolist (c (split-string match ", ?"))
	      (push c commands)))))
      (sort commands #'string<))))

(defconst pcmpl-hg-commands (pcmpl-hg-commands)
  "List of `hg' commands.")

(defun pcomplete/hg ()
  "Completion for `hg'."
  ;; Completion for the command argument.
  (pcomplete-here* pcmpl-hg-commands)
  (cond
   ((pcomplete-match "help" 1)
    (pcomplete-here* pcmpl-hg-commands))
   (t
    (while (pcomplete-here (pcomplete-entries))))))
#+end_src


*** If we pcomplete. lets auto complete

#+begin_src emacs-lisp
    (emagician/def-named-hook emagician/ac-setup-for-eshell-mode shell-mode-hook
      (setq ac-sources 
            '(ac-source-pcomplete ac-source-filename ac-source-files-in-current-dir ac-source-words-in-buffer)))
#+end_src

* MaGit 												:keybinding:external:
  
#+begin_src emacs-lisp
  
  (emagician-expect-package 'magit)
  (emagician-expect-package 'git-commit-mode)
  (emagician-expect-package 'gitconfig-mode)
  (emagician-expect-package 'gitignore-mode)
  
  (emagician-expect-package 'helm-ls-git)
    
  (when (locate-file "git-achievements" exec-path)
    (setq magit-git-executable "git-achievements"))
  (global-set-key (kbd "C-c m") 'magit-status)
#+end_src

** Git Messaging...?

#+begin_src emacs-lisp
(emagician-expect-package 'git-messenger)
#+end_src

** Gutter

#+begin_src emacs-lisp
(emagician-expect-package 'git-gutter)
(global-git-gutter-mode t)

(global-set-key (kbd "C-x v =") 'git-gutter:popup-hunk)

;; Jump to next/previous hunk
(global-set-key (kbd "C-x v p") 'git-gutter:previous-hunk)
(global-set-key (kbd "C-x v n") 'git-gutter:next-hunk)

;; Stage current hunk
(global-set-key (kbd "C-x v s") 'git-gutter:stage-hunk)

;; Revert current hunk
(global-set-key (kbd "C-x v r") 'git-gutter:revert-hunk)


(setq git-gutter:window-width 1)
(setq git-gutter:added-sign "++ ")
(setq git-gutter:deleted-sign "-- ")
(setq git-gutter:modified-sign "== ")

(set-face-foreground 'git-gutter:modified "purple") ;; background color
(set-face-foreground 'git-gutter:added "green")
(set-face-foreground 'git-gutter:deleted "red")
#+end_src

#+RESULTS:

** Ma-Git-Hub													  :depricate:

   We wan't to use SSL with github.  Thanks. 

   There is a bug with magit currently. Disabled.

#+begin_src emacs-lisp :tangle no
  (emagician-expect-package 'magithub)
  (setq magithub-use-ssl t)
#+end_src

** NEXT Set up git help with info using emacs. 


* Edit with emacs
#+begin_src emacs-lisp
  (when (require 'edit-server nil t)
    (edit-server-start))
#+end_src
  

* Emacs server
#+begin_src emacs-lisp
(server-start)
#+end_src
  

* Info diectory
** Set up Var
#+begin_src emacs-lisp 
;(add-to-list 'Info-additional-directory-list (expand-file-name "dist/info" emagician-dir))
#+end_src

** Dir writing and tangling

*** Head of the thing
#+begin_src texinfo :tangle dist/info/dir :noweb yes :padline no 
  This is the file .../info/dir, which contains the
  topmost node of the Info hierarchy, called (dir)Top.
  The first time you invoke Info you start off looking at this node.
  ^_
  File: dir,      Node: Top

  Emagician Info Directory
  ************************

    The info files here are distributed under various open licenses.  
  
    Please be sure to check the individual licenses for each software
    package.
  
   * Menu:
  
  <<info-dir-autogenerated>>
  
  ---- User's `dir' ends here ----
#+end_src

*** Code to emit info-dir-files

#+begin_src emacs-lisp :tangle no :results value wrap  
  (apply 'concat 
         "#+name: info-dir-autogenerated\n"
         "#+begin_src texinfo :tangle no\n" 
         "  This listing was autogenerated on: " (current-time-string) "\n"
         "  -----------------------------------------------------------\n\n"
         (append (mapcar '(lambda (f) 
                            (cond ((string= f "dir")
                                   "")
                                  ((string= (substring f 0 1) ".")
                                   "")
                                  (t
                                   (concat "  * " f "(" f ")          Docco!\n") )))
                         (directory-files (expand-file-name "dist/info" emagician-dir)))
                 '("#+end_src" "\n")))
#+end_src

#+RESULTS:
:RESULTS:
#+name: info-dir-autogenerated
#+begin_src texinfo :tangle no
  This listing was autogenerated on: Thu Oct 11 00:27:30 2012
  -----------------------------------------------------------

  * git.info(git.info)          Docco!
  * gitman.info(gitman.info)          Docco!
  * r5rs.info(r5rs.info)          Docco!
#+end_src
:END:


* ERC + Bitlbee - IM with emacs. 

#+begin_src emacs-lisp
  (eval-after-load 'erc
    '(progn
       (add-to-list 'erc-modules 'log)
	   (add-to-list 'erc-modules 'autoaway)
	   (add-to-list 'erc-modules 'page)
	   (add-to-list 'erc-modules 'spelling) 
       
       (erc-update-modules)))
  
  (setq erc-log-channels-directory (concat "~/Dropbox/Documents/im/" system-name))
  (setq erc-save-buffer-on-part t
        erc-log-write-after-send t
        erc-log-write-after-insert t)
  
  (setq erc-hide-list '("MODE" "JOIN" "PART" "QUIT"))
#+end_src

** NEXT move the log into my personal config 


* Processes through dizzee

  I don't have a great example of htis yet.. but maybe soon.  This package is The Shit[fn:1] though.

#+begin_src emacs-lisp
(emagician-expect-package 'dizzee)
#+end_src

** TODO Move this into interface? 


* RSS reading through Newsticker								   :disabled:
** TODO re-enable this if it can stop sucking
** Set up emagician feeds
   This is set up as it's own variable, so you can add it. Because, hopefully it's useful.

#+begin_src emacs-lisp :tangle no
  (setq emagician/newsticker-url-list 
  '(("Emacs Wiki" "http://www.emacswiki.org/emacs/?action=rss")
    ("Reddit" "http://www.reddit.com/r/emacs/.rss") ; I've never seen a more wretched hive of scum and mysogeny.  but /r/emacs is mostly okay.
    ("Worg" "http://orgmode.org/w/?p=worg.git;a=rss;opt=--no-merges")
    ("Stack Overflow" "http://stackoverflow.com/feeds/tag?tagnames=emacs&sort=newest")
    ("MELPA Twitter Feed" "http://api.twitter.com/1/statuses/user_timeline.rss?screen_name=melpa_emacs")
    ("Emacs Haiku" "http://api.twitter.com/1/statuses/user_timeline.rss?screen_name=EmacsHaiku")
  ))
  
  (setq emagician/newsticker-productivity 
        '(("Buffer... not sure about this one" "http://blog.bufferapp.com/"))
        )
  
#+end_src

** Set up newsticker proper
*** TODO set up external dependency here for w3m				   :external:
#+begin_src emacs-lisp :tangle no
  (require 'newsticker)
  
  ; W3M HTML renderer isn't essential, but it's pretty useful.
  (emagician-expect-package 'w3m)
  (require 'w3m)
  
  (setq newsticker-html-renderer 'w3m-region)
  
  ; We want our feeds pulled every 10 minutes.
  (setq newsticker-retrieval-interval 600)
  
  ; Setup the feeds. We'll have a look at these in just a second.
  (setq newsticker-url-list-defaults nil)
  (setq newsticker-url-list emagician/newsticker-url-list)
  
  ; Optionally bind a shortcut for your new RSS reader.
  (global-set-key (kbd "C-c r") 'emagician/newsticker-start-and-view)
  
  (defun emagician/newsticker-start-and-view ()
    "Start and launch newsticker."
    (interactive)
    (newsticker-start t)
    (newsticker-treeview))
#+end_src



* Footnotes
[fn:1] "The Shit" is a good thing.  

[fn:2]  https://tsdh.wordpress.com/2013/05/31/eshell-completion-for-git-bzr-and-hg/
 
