using System;
using SCG = System.Collections.Generic;

using Nemerle.Assertions;
using Nemerle.Utility;
using Nemerle.Compiler;
using Nemerle.Collections;

using PT = Nemerle.Compiler.Parsetree;
using PEG = Nemerle.Peg;
using CS = CSharpParser;

namespace CSharpToNemerle
{
  public partial class AstConverter
  {
    ConvertTopDeclaration(decl : CS.TypeDeclaration) : void
    {
      typeDecls.Add(ConvertTypeDeclaration(decl))
    }

    ConvertModifiers(attributes : list[CS.CustomAttribute], modifiers : list[CS.Modifier]) : AttributesAndModifiers
    {
      def flags = modifiers.FoldLeft(NemerleModifiers.None, fun(flag : CS.Modifier, flags : NemerleModifiers) {
        def setFlag(value) {
          when(flags %&& value)
            Message.Error(flag |> NLocation, $"duplicated modifier `$flag'");
          flags %| value
        }
        match(flag) {
          | New         => setFlag(NemerleModifiers.New)
          | Public      => setFlag(NemerleModifiers.Public)
          | Protected   => setFlag(NemerleModifiers.Protected)
          | Internal    => setFlag(NemerleModifiers.Internal)
          | Private     => setFlag(NemerleModifiers.Private)
          | Partial     => setFlag(NemerleModifiers.Partial)
          | Virtual     => setFlag(NemerleModifiers.Virtual)
          | Volatile    => setFlag(NemerleModifiers.Volatile)
          | Static      => setFlag(NemerleModifiers.Static)
          | Sealed      => setFlag(NemerleModifiers.Sealed)
          | Override    => setFlag(NemerleModifiers.Override)
          | Abstract    => setFlag(NemerleModifiers.Abstract)
          | Extern      => setFlag(NemerleModifiers.Extern)
          | Unsafe      =>
            Message.Error(flag |> NLocation, "unsafe code is not supported");
            flags
          | Readonly =>
            flags //TODO: deal with Readonly
        }
      });
      // it is strange, but Nemerle does not save target of attribute
      def attrs = attributes.Map(
        atb => PT.PExpr.Call(atb |> NLocation, ConvertExpr(atb.Name), atb.Args.Map(ConvertExpr)));

      AttributesAndModifiers(flags, attrs)
    }

    ConvertTyparms(tyvars : list[CS.TypeParameter], constClauses : list[CS.TypeParameterConstraintClause]) : PT.Typarms
    {
      | ([], []) => PT.Typarms.Empty
      | _ =>
        def (tyvars, varianceConstraints) = tyvars.FoldRight(([], []),
          fun(tp, (tyVars, constraints)) {
            match(tp.Attributes) {
              | [] => ()
              | _ :: _ =>
                Message.Warning(tp.Name |> NLocation, "type parameter attributes are ignored");
            }
            def tyVar = MakeSplicableName(tp.Name);
            match(tp.Variance : CS.TypeParameterVariance) {
              | In as variance with varianceId = "-"
              | Out as variance with varianceId = "+" =>
                def loc = variance |> NLocation;
                def constraint = PT.Constraint(tyVar, PT.PExpr.Ref(loc, MakeName(varianceId, loc)), true);
                (tyVar :: tyVars, constraint :: constraints)
              | _ =>
                (tyVar :: tyVars, constraints)
            }
          });

        def constraints = varianceConstraints :: constClauses.Map(fun(constrClause) {
          def tyVar = MakeSplicableName(constrClause.ParameterName);
          constrClause.Constraints.Map(
            fun(c)
            {
              def loc = c |> NLocation;
              match(c : CS.TypeParameterConstraint)
              {
                | DefaultCtor   => PT.Constraint(tyVar, PT.PExpr.Ref(loc, MakeName("new", loc)), true);
                | Struct        => PT.Constraint(tyVar, PT.PExpr.Ref(loc, MakeName("struct", loc)), true);
                | Class         => PT.Constraint(tyVar, PT.PExpr.Ref(loc, MakeName("class", loc)), true);
                | Base(typeRef) => PT.Constraint(tyVar, ConvertExpr(typeRef), false);
              };
            })
        });

        PT.Typarms(tyvars, constraints.Flatten())
    }

    ConvertTypeDeclaration(decl : CS.TypeDeclaration) : PT.TopDeclaration
    {
      def loc = decl |> NLocation;
      mutable pool = PrepareAttributes(decl.Attributes);
      def mods = ConvertModifiers(TakeOutAttributes.[CS.CustomAttributeTarget.Type](ref pool), decl.Modifiers);
      def name = MakeSplicableName(decl.Name);
      def typarms = ConvertTyparms(decl.TypeParameters, decl.Constraints);

      def convertTypeMembers(members, typeName) {
        def buffer = SCG.List(30);
        foreach(member in members)
        {
          def newScope = LabelScope(manager, locationConverter);
          WithNewLabelScope(newScope).ConvertTypeMember(member, typeName, buffer);
          newScope.CheckLabels();
        }
        NList.ToList(buffer)
      }

      def nDecl = match(decl) {
        | Class(baseTypes, members) =>
          PT.TopDeclaration.Class(loc, name, mods, typarms, baseTypes.Map(ConvertExpr), convertTypeMembers(members, decl.Name))

        | Struct(baseTypes, members) =>
          mods.mods |= NemerleModifiers.Struct;
          PT.TopDeclaration.Class(loc, name, mods, typarms, baseTypes.Map(ConvertExpr), convertTypeMembers(members, decl.Name))

        | Interface(baseTypes, members) =>
          PT.TopDeclaration.Interface(loc, name, mods, typarms, baseTypes.Map(ConvertExpr), convertTypeMembers(members, decl.Name))

        | Delegate(returnType, formalParameters) =>
          def returnValueMods = ConvertModifiers(TakeOutAttributesWithoutDefault.[CS.CustomAttributeTarget.Return](ref pool), []);
          def header = PT.PFunHeader(loc, typarms, name, ConvertExpr(returnType), formalParameters.Map(ConvertParam));
          def nDelegateDecl = PT.TopDeclaration.Delegate(loc, name, mods, typarms, header);
          nDelegateDecl.ReturnValueModifiers = returnValueMods;
          nDelegateDecl

        | Enum(null, members) =>
          PT.TopDeclaration.Enum(loc, name, mods, typarms, [], convertTypeMembers(members, decl.Name))

        | Enum(baseType, members) =>
          PT.TopDeclaration.Enum(loc, name, mods, typarms, [ConvertExpr(baseType)], convertTypeMembers(members, decl.Name))
      }
      CheckAttributes(pool);
      ResetDocComments(name);
      nDecl
    }

    ConvertTypeMember(member : CS.TypeMember, typeName : CS.Identifier, buffer : SCG.List[PT.ClassMember]) : void
    {
      match(member) {
        | Constant(memberType, constants) =>
          foreach((loc, name, value) in constants) {
            def loc = loc |> NLocation;
            def name = MakeSplicableName(name);
            mutable pool = PrepareAttributes(member.Attributes);

            def value = ConvertExpr(value);
            def mods = ConvertModifiers(TakeOutAttributes.[CS.CustomAttributeTarget.Field](ref pool), member.Modifiers);
            mods.AddCustomAttribute(Util.locate(loc, <[ Nemerle.InternalMacros.StaticInitializer($value) ]>));
            mods.mods |= NemerleModifiers.Static;

            def member = PT.ClassMember.Field(loc, name, mods, ConvertExpr(memberType), value);
            CheckAttributes(pool);
            ResetDocComments(name);
            member |> WithCurrentEnv |> buffer.Add;
          } otherwise
            Message.Error(member |> NLocation, "bad constant declaration");

        | Field(memberType, fields) =>
          foreach((loc, name, init) in fields) {
            def memberTypeExpr = ConvertExpr(memberType);
            def loc = loc |> NLocation;
            def name = MakeSplicableName(name);
            mutable pool = PrepareAttributes(member.Attributes);

            def mods = ConvertModifiers(TakeOutAttributes.[CS.CustomAttributeTarget.Field](ref pool), member.Modifiers);
            unless(member.Modifiers.Exists(_ is CS.Modifier.Readonly))
              mods.mods |= NemerleModifiers.Mutable;
            match(ConvertVariableInitializer(memberType, init)) {
              | null => ()
              | value =>
                mods.AddCustomAttribute(Util.locate(loc, <[ Nemerle.InternalMacros.StaticInitializer($value) ]>));
            }
            def member = PT.ClassMember.Field(loc, name, mods, memberTypeExpr);
            CheckAttributes(pool);
            ResetDocComments(name);
            member |> WithCurrentEnv |> buffer.Add;
          } otherwise
            Message.Error(member |> NLocation, "bad field declaration");

        | FixedSizeBuffer =>
          Message.Error(member |> NLocation, "fixed size buffers are not supported");

        | Method(CS.Expr.Ref(id),                             returnType, formalParameters, typeParameters, constraints, body) with iface = null
        | Method(CS.Expr.Member(_, CS.Expr.Ref(id)) as iface, returnType, formalParameters, typeParameters, constraints, body)
        | Method(_,                                           returnType, formalParameters, typeParameters, constraints, body) with (id = null, iface = null) =>
          def loc = member |> NLocation;
          mutable pool = PrepareAttributes(member.Attributes);

          def mods = ConvertModifiers(TakeOutAttributes.[CS.CustomAttributeTarget.Method](ref pool), member.Modifiers);
          def returnValueMods = ConvertModifiers(TakeOutAttributesWithoutDefault.[CS.CustomAttributeTarget.Return](ref pool), []);
          def name = match(id) {
            | null => Message.FatalError(loc, "bad method name")
            | _ =>
              match (iface)
              {
                | null => MakeSplicableName(id)
                | CS.Expr.Member(expr, _) => MakeSplicableName($"$expr.$id", iface |> NLocation)
              }
          }
          def header = PT.PFunHeader(loc, ConvertTyparms(typeParameters, constraints), name, ConvertExpr(returnType), formalParameters.Map(ConvertParam));
          def iface = match(iface) {
            | null => []
            | _ => [ConvertExpr(iface)]
          }
          def body = match(body) {
            | Empty => FunBody.Abstract()
            | _ => FunBody.Parsed(ConvertExpr(body))
          }
          def method = PT.ClassMember.Function(loc, name, mods, header, iface, body);
          method.ReturnValueModifiers = returnValueMods;
          CheckAttributes(pool);
          ResetDocComments(name);
          method |> WithCurrentEnv |> buffer.Add;

        | Property(CS.Expr.Ref(id),                               memberType,                   getter, setter) with (iface = null,             formalParameters = [])
        | Property(CS.Expr.Member(_, CS.Expr.Ref(id)) as iface,   memberType,                   getter, setter) with (                          formalParameters = [])
        | Property(_,                                             memberType,                   getter, setter) with (iface = null, id = null,  formalParameters = [])
        | Indexer(CS.Expr.This as th,                             memberType, formalParameters, getter, setter) with (iface = null, id = CS.Identifier(th.Location, "Item"))
        | Indexer(CS.Expr.Member(_, CS.Expr.This as th) as iface, memberType, formalParameters, getter, setter) with (              id = CS.Identifier(th.Location, "Item"))
        | Indexer(_,                                              memberType, formalParameters, getter, setter) with (iface = null, id = null) =>
          def loc = member |> NLocation;
          mutable pool = PrepareAttributes(member.Attributes);

          def mods = ConvertModifiers(TakeOutAttributes.[CS.CustomAttributeTarget.Property](ref pool), member.Modifiers);
          def strName = match(id) {
            | null => Message.FatalError(loc, "bad property name")
            | _ =>
              match (iface)
              {
                | null => id.Id
                | CS.Expr.Member(expr, _) => $"$expr.$id"
              }
          }
          def name = MakeSplicableName(strName, id |> NLocation);
          def memberType = ConvertExpr(memberType);
          def iface = 
            match(iface)
            {
              | null => []
              | Member(expr, CS.Expr.This as n) => [<[ $(ConvertExpr(expr)).$(MakeSplicableName("Item", n |> NLocation)) ]>]
              | _ => [ConvertExpr(iface)]
            };
          def parms = formalParameters.Map(ConvertParam);

          mutable accessorPool = []; // we cant pass ref-values to local functions

          def convertAccessor(accessor, prefix, returnType, parms) {
            def loc = accessor |> NLocation;
            def attrs = TakeOutAttributesWithoutDefault.[CS.CustomAttributeTarget.Method](ref pool)
                      + TakeOutAttributes.[CS.CustomAttributeTarget.Method](ref accessorPool);
            def returnValueAttrs = TakeOutAttributesWithoutDefault.[CS.CustomAttributeTarget.Return](ref pool)
                                 + TakeOutAttributesWithoutDefault.[CS.CustomAttributeTarget.Return](ref accessorPool);
            def mods = ConvertModifiers(attrs, accessor.Modifiers);
            def returnValueMods = ConvertModifiers(returnValueAttrs, []);
            def name = MakeSplicableName(prefix + strName, id |> NLocation);
            def header = PT.PFunHeader(loc, name, returnType, parms);
            def body= match(accessor.Body : CS.Statement) {
              | Empty => FunBody.Abstract()
              | _     => FunBody.Parsed(ConvertExpr(accessor.Body))
            }
            CheckAttributes(accessorPool);
            def accessor = PT.ClassMember.Function(loc, name, mods, header, iface, body);
            accessor.ReturnValueModifiers = returnValueMods;
            accessor |> WithCurrentEnv
          }

          def getter = match(getter) {
            | null => None()
            | _ =>
              accessorPool = PrepareAttributes(getter.Attributes);
              convertAccessor(getter, "get_", memberType, parms) |> Some
          }

          def setter = match(setter) {
            | null => None()
            | _  =>
              accessorPool = PrepareAttributes(setter.Attributes);
              def setterLoc = setter |> NLocation;
              convertAccessor(setter, "set_", PT.PExpr.Void(setterLoc),
                parms + [ PT.PParameter(
                    loc   = setterLoc,
                    name  = MakeSplicableName("value", setterLoc),
                    ty    = memberType,
                    modifiers = {
                      def attrs = TakeOutAttributesWithoutDefault.[CS.CustomAttributeTarget.Param](ref accessorPool);
                      def mods = ConvertModifiers(attrs, []);
                      mods.mods |= NemerleModifiers.Mutable;
                      mods }) ]) |> Some
          }
          
          def member = PT.ClassMember.Property(loc, name, mods, memberType, parms, getter, setter, iface);
          CheckAttributes(pool);
          ResetDocComments(name);
          member |> WithCurrentEnv |> buffer.Add;

        | Event(memberType, adder, remover, events) =>
          foreach((loc, name, init) in events) {
            def loc = loc |> NLocation;
            mutable pool = PrepareAttributes(member.Attributes);

            def mods = ConvertModifiers(TakeOutAttributes.[CS.CustomAttributeTarget.Event](ref pool), member.Modifiers);
            def (name, id, iface) = match(name : CS.Expr) {
              | Ref(id)                 => (MakeSplicableName(id), id, [])
              | Member(iface, Ref(id))  => (MakeSplicableName(id), id, [ConvertExpr(iface)])
              | _                       => Message.FatalError(loc, "bad event name")
            }
            def memberType = ConvertExpr(memberType);
            when(null != init)
              Message.Error(init |> NLocation, "event cannot have initial value");

            def createEventField() {
              def name =
                if(manager.IsIntelliSenseMode)
                  MakeSplicableName("_N_event_field_of_" + id.Id, id |> NLocation);
                else
                  MakeSplicableName(Util.tmpname("event_field_of_" + id.Id), id |> NLocation);
              def attrs = TakeOutAttributesWithoutDefault.[CS.CustomAttributeTarget.Field](ref pool);
              def mods = ConvertModifiers(attrs, []);
              mods.mods = NemerleModifiers.Private %| NemerleModifiers.Mutable;
              PT.ClassMember.Field(loc, name, mods, memberType) |> WithCurrentEnv
            }
            def createEventAccessor(prefix) {
              def name = MakeSplicableName(prefix + id.Id, id |> NLocation);
              def attrs = TakeOutAttributesWithoutDefault.[CS.CustomAttributeTarget.Method](ref pool);
              def mods = ConvertModifiers(attrs, []);
              def header = PT.PFunHeader(
                loc         = loc,
                name        = name,
                returnType  = PT.PExpr.Void(loc),
                parameters  = [ PT.PParameter(
                    loc   = loc,
                    name  = MakeSplicableName("value", loc),
                    ty    = memberType,
                    modifiers = {
                      def attrs = TakeOutAttributesWithoutDefault.[CS.CustomAttributeTarget.Param](ref pool);
                      def mods = ConvertModifiers(attrs, []);
                      mods.mods |= NemerleModifiers.Mutable;
                      mods }) ]);
              PT.ClassMember.Function(loc, name, mods, header, iface, null) |> WithCurrentEnv
            }
            def convertEventAccessor(accessor, prefix) {
              def loc = accessor |> NLocation;
              unless(accessor.Modifiers.IsEmpty)
                Message.Error(loc, "event accessor cannot have any modifiers");
              mutable accessorPool = PrepareAttributes(accessor.Attributes);
              def attrs = TakeOutAttributesWithoutDefault.[CS.CustomAttributeTarget.Method](ref pool)
                        + TakeOutAttributes.[CS.CustomAttributeTarget.Method](ref accessorPool);
              def mods = ConvertModifiers(attrs, []);
              def name = MakeSplicableName(prefix + id.Id, id |> NLocation);
              def header = PT.PFunHeader(
                loc         = loc,
                name        = name,
                returnType  = PT.PExpr.Void(loc),
                parameters  = [ PT.PParameter(
                    loc   = loc,
                    name  = MakeSplicableName("value", loc),
                    ty    = memberType,
                    modifiers = {
                      def attrs = TakeOutAttributesWithoutDefault.[CS.CustomAttributeTarget.Param](ref accessorPool);
                      def mods = ConvertModifiers(attrs, []);
                      mods.mods |= NemerleModifiers.Mutable;
                      mods }) ]);
              def body = match(accessor.Body : CS.Statement) {
                | Empty => FunBody.Abstract()
                | _     => FunBody.Parsed(ConvertExpr(accessor.Body))
              }
              CheckAttributes(accessorPool);
              PT.ClassMember.Function(loc, name, mods, header, iface, body) |> WithCurrentEnv
            }
  
            def (field, adder, remover) = match(adder, remover) {
              | (null, null) =>
                (createEventField(), createEventAccessor("add_"), createEventAccessor("remove_"))
  
              | (null, _)
              | (_, null) =>
                Message.Error(loc, "both of event accessors `add' and `remove' must be specified");
                (createEventField(), createEventAccessor("add_"), createEventAccessor("remove_"))
  
              | _ =>
                (null, convertEventAccessor(adder, "add_"), convertEventAccessor(remover, "remove_"))
            }

            def member = PT.ClassMember.Event(loc, name, mods, memberType, field, adder, remover);
            CheckAttributes(pool);
            ResetDocComments(name);
            member |> WithCurrentEnv |> buffer.Add;
          } otherwise
            Message.Error(member |> NLocation, "bad event declaration");

        | Operator(id, returnType, formalParameters, body) =>
          def loc = member |> NLocation;
          mutable pool = PrepareAttributes(member.Attributes);

          //TODO: check overloadable operator name and args
          def mods = ConvertModifiers(TakeOutAttributes.[CS.CustomAttributeTarget.Method](ref pool), member.Modifiers);
          def returnValueMods = ConvertModifiers(TakeOutAttributesWithoutDefault.[CS.CustomAttributeTarget.Return](ref pool), []);
          def name = 
						match (id.Id)
						{
							| "implicit" => MakeSplicableName(":", id |> NLocation)
							| "explicit" => MakeSplicableName(":>", id |> NLocation)
							| _ => MakeSplicableName(id)
						};
          def header = PT.PFunHeader(loc, PT.Typarms.Empty, name, ConvertExpr(returnType), formalParameters.Map(ConvertParam));
          def body = match(body) {
            | Empty => FunBody.Abstract()
            | _ => FunBody.Parsed(ConvertExpr(body))
          }

          def member = PT.ClassMember.Function(loc, name, mods, header, [], body);
          member.ReturnValueModifiers = returnValueMods;
          CheckAttributes(pool);
          ResetDocComments(name);
          member |> WithCurrentEnv |> buffer.Add;

        | Constructor(CS.Expr.Ref(id),  formalParameters, init, body)
        | Constructor(_,                formalParameters, init, body) with id = null =>
          def loc = member |> NLocation;
          mutable pool = PrepareAttributes(member.Attributes);

          def mods = ConvertModifiers(TakeOutAttributes.[CS.CustomAttributeTarget.Method](ref pool), member.Modifiers);
          def name = match(id) {
            | null => Message.FatalError(loc, "bad constructor name")
            | _ =>
              def idLoc = id |> NLocation;
              when(id.Id != typeName.Id)
                Message.Error(idLoc, "constructor name must be equal to type name");
              MakeSplicableName(".ctor", idLoc)
          }
          def header = PT.PFunHeader(loc, PT.Typarms.Empty, name, PT.PExpr.Void(loc), formalParameters.Map(ConvertParam));
          def body = match(body, init) {
            | (Empty, Default) => FunBody.Abstract()
            | _ =>
              def initLoc = init |> NLocation;
              match(init) {
                | Default =>
                  FunBody.Parsed(ConvertExpr(body))
                | This(args)
                  with otherCtor = PT.PExpr.Call(initLoc, PT.PExpr.This(initLoc), args.Map(ConvertExpr))
                | Base(args)
                  with otherCtor = PT.PExpr.Call(initLoc, PT.PExpr.Base(initLoc), args.Map(ConvertExpr)) =>
                  FunBody.Parsed(PT.PExpr.Sequence(body |> NLocation, [otherCtor, ConvertExpr(body)]))
              }
          }
          def member = PT.ClassMember.Function(loc, name, mods, header, [], body);
          CheckAttributes(pool);
          ResetDocComments(name);
          member |> WithCurrentEnv |> buffer.Add;

        | Destructor(CS.Expr.Ref(id), formalParameters, body)
        | Destructor(_,               formalParameters, body) with id = null =>
          def loc = member |> NLocation;
          mutable pool = PrepareAttributes(member.Attributes);

          //TODO: check dtor modifiers
          def mods = ConvertModifiers(TakeOutAttributes.[CS.CustomAttributeTarget.Method](ref pool), member.Modifiers);
          mods.mods |= NemerleModifiers.Override %| NemerleModifiers.Protected;
          def name = match(id) {
            | null => Message.FatalError(loc, "bad destructor name")
            | _ =>
              def idLoc = id |> NLocation;
              when(id.Id != typeName.Id)
                Message.Error(idLoc, "destructor name must be equal to type name");
              MakeSplicableName("Finalize", idLoc)
          }
          def header = PT.PFunHeader(loc, PT.Typarms.Empty, name, PT.PExpr.Void(loc), formalParameters.Map(ConvertParam));
          def body = match(body) {
            | Empty => FunBody.Abstract()
            | _ => FunBody.Parsed(ConvertExpr(body))
          }

          def member = PT.ClassMember.Function(loc, name, mods, header, [], body);
          CheckAttributes(pool);
          ResetDocComments(name);
          member |> WithCurrentEnv |> buffer.Add;

        | EnumOption(name, null) =>
          def loc = member |> NLocation;
          def name = MakeSplicableName(name);
          mutable pool = PrepareAttributes(member.Attributes);
          def mods = ConvertModifiers(TakeOutAttributes.[CS.CustomAttributeTarget.Field](ref pool), member.Modifiers);

          def member = PT.ClassMember.EnumOption(loc, name, mods, None());
          CheckAttributes(pool);
          ResetDocComments(name);
          member |> WithCurrentEnv |> buffer.Add;


        | EnumOption(name, value) =>
          def loc = member |> NLocation;
          def name = MakeSplicableName(name);
          mutable pool = PrepareAttributes(member.Attributes);
          def mods = ConvertModifiers(TakeOutAttributes.[CS.CustomAttributeTarget.Field](ref pool), member.Modifiers);

          def member = PT.ClassMember.EnumOption(loc, name, mods, Some(ConvertExpr(value)));
          CheckAttributes(pool);
          ResetDocComments(name);
          member |> WithCurrentEnv |> buffer.Add;


        | TypeDeclaration(decl) =>
          def ndecl = ConvertTypeDeclaration(decl);
          def member = PT.ClassMember.TypeDeclaration(ndecl.Location, ndecl.name, ndecl.modifiers, ndecl);
          member |> WithCurrentEnv |> buffer.Add;
      }
    }

    ConvertParam(parm : CS.FormalParameter) : PT.PParameter
    {
      def loc = parm |> NLocation;
      def parmName = MakeSplicableName(parm.Name);
      def parmMods = ConvertModifiers(TakeOutAndCheckAttributes.[CS.CustomAttributeTarget.Param](parm.Attributes), []);
      parmMods.mods |= NemerleModifiers.Mutable;
      mutable parmTy =
        match(parm.ParameterType) {
          | null  => PT.PExpr.Wildcard(loc)
          | ty    => ConvertExpr(ty)
        }
      match(parm.Modifier) {
        | Normal  => ()
        | This    => parmMods.custom_attrs ::= Util.locate(loc, <[ Nemerle.Core._N_ExtensionMethodOnThisParameter ]>);
        | Ref     => parmTy = PT.PExpr.ParmByRef(loc, parmTy);
        | Out     => parmTy = PT.PExpr.ParmOut(loc, parmTy);
        | Params  => parmMods.custom_attrs ::= Util.locate(loc, <[ System.ParamArrayAttribute ]>);
      }
      
      def defaultValue = if (parm.DefaultValue == null) None() else Some(ConvertExpr(parm.DefaultValue));

      PT.PParameter(loc, parmName, parmTy, parmMods, defaultValue)
    }
  }
}