using System;
using System.Runtime.InteropServices;
using System.Xml.Linq; // Пространство имен LINQ to XML
using Nemerle.Collections;
using Nemerle.Late;
using Nemerle.Utility;

namespace NReporter
{
    /// <summary>
    /// Description of ExcelPrinter.
    /// </summary>
  class ExcelPrinter : IPrinter
  {
    public this() { late _app.Visible = false }
    _app : object = LateEx.CreateObject("Excel.Application");
    mutable _worksheets : list[string * int];
    
    public ReadTemplateInfo(info : XElement) : void
    {
      // Читаем информацию о том какие страницы (Worksheet) и в каком
      // количестве копий нужно распечатать.
      _worksheets = 
        if (info == null) []
        else info.Elements("Worksheet").Map(
            ws : XElement => 
              (ws.Value, 
               if (ws.HasAttributes) ws.Attribute("copies") :> int else 1));
    }

    /// Открывает файл в Excel и печатает его содержимое.
    public Print(reportPath : string) : void
    {
      def workbook = late _app.Workbooks.Open(reportPath);
      
      foreach ((worksheetName, copies) in _worksheets)
        _ = late workbook.Worksheets[worksheetName].PrintOut(Copies=copies);

      _ = late workbook.Close(false); // Закрываем workbook
    }

    public Dispose() : void
    {
      _ = late _app.Quit();
      _ = Marshal.ReleaseComObject(_app); // освобождаем ссылку на COM-объект
    }
  }
}
