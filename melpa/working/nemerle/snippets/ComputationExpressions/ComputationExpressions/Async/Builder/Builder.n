using System;
using System.Collections;
using System.Collections.Generic;

namespace Nemerle.ComputationExpressions.Async
{
  public module AcyncBuilder
  {
    public Return[A](value : A) : Async[A]
    {
      AsyncReturn(value);
    }

    public Bind[A, B](async : Async[A], f : A -> Async[B]) : Async[B]
    {
      AsyncBind(async, f);
    }

    public Bind[A, B](async : Async[A], f : void -> Async[B]) : Async[B]
    {
      AsyncBindVoid(async, f);
    }

    public Bind[A, B](async : AsyncValue[A], f : A -> Async[B]) : Async[B]
    {
      AsyncBindValue(async, f);
    }

    public Bind[A, B](async : AsyncValue[A], f : void -> Async[B]) : Async[B]
    {
      AsyncBindValueVoid(async, f);
    }

    public While(pred : void -> bool, body : void -> Async[FakeVoid]) : Async[FakeVoid]
    {
      AsyncWhile(pred, body);
    }

    public ForEach[A](coll : IEnumerable[A], body : A -> Async[FakeVoid]) : Async[FakeVoid]
    {
      AsyncForEach(coll.GetEnumerator(), body);
    }

    public ForEach[A](coll : IEnumerable, body : A -> Async[FakeVoid]) : Async[FakeVoid]
    {
      AsyncForEachObject(coll.GetEnumerator(), body);
    }

    public Delay[A](cont : void -> Async[A]) : Async[A]
    {
      AsyncDelay(cont);
    }

    public TryFinally[A](body : Async[A], handler : void -> void) : Async[A]
    {
      AsyncTryFinally(body, handler);
    }

    public TryCatch[A](body : Async[A], handler : Exception -> Async[A]) : Async[A]
    {
      AsyncTryCatch(body, handler);
    }

    public Using[A, B] (resource : A, cont : A -> Async[B]) : Async[B]
      where A : IDisposable
    {
      TryFinally(Delay(() => cont(resource)), () =>
        {
          def resource = resource : IDisposable;
          when (resource != null) 
            resource.Dispose()
        })
    }
  }
}
