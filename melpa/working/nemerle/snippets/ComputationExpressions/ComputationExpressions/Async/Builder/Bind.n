namespace Nemerle.ComputationExpressions.Async
{
  [Record]
  internal class AsyncBind[A, B] : Async[B]
  {
    private mutable async : Async[A];
    private cont : A -> Async[B];

#pragma warning disable 649
    [RecordIgnore]
    private mutable _r : A;
#pragma warning restore

    public override Exec(job : AsyncJobBase, _res : ref B) : Async[B]
    {
      async = job.ExecWhileReady(async, ref _r);
      if (async == null && job.IsReady)
        cont(_r);
      else
        this;
    }

    public override Cancel() : void
    {
      when (async != null)
        async.Cancel();
    }
  }

  [Record]
  internal class AsyncBindVoid[A, B] : Async[B]
  {
    private mutable async : Async[A];
    private cont : void -> Async[B];

    public override Exec(job : AsyncJobBase, _res : ref B) : Async[B]
    {
      mutable r;
      async = job.ExecWhileReady(async, ref r);
      if (async == null && job.IsReady)
        cont();
      else
        this;
    }

    public override Cancel() : void
    {
      when (async != null)
        async.Cancel();
    }
  }
}
