using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;

namespace Nemerle.ComputationExpressions.Async
{
  public class CancelationToken
  {
    public static Default : CancelationToken { get { null } }

    private _syncroot : object = object();
    private _jobs : List[WeakReference] = List();
    internal Register(job : AsyncJobBase) : void
    {
      lock (_syncroot)
      {
        _jobs.Add(WeakReference(job));
      }
    }

    public Cancel() : void
    {
      def aggregateException = AsyncAggregateException();
      lock (_syncroot)
      {
        foreach (job in _jobs)
        {
          def job = job.Target :> AsyncJobBase;
          when (job != null)
            try
            {
              job.Cancel();
            }
            catch
            {
              | ex => aggregateException.Add(ex);
            }
        }
        _jobs.Clear();
      }
      aggregateException.ThrowIfAny();
    }
  }
}
