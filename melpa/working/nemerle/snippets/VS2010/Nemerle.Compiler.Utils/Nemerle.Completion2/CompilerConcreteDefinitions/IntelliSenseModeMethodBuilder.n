using System;
using Nemerle.Assertions;
using Nemerle.Compiler;
using Nemerle.Utility;
using Nemerle.Collections;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Typedtree;
using System.Diagnostics;
using Nemerle.Compiler.Utils;

using Typed = Nemerle.Compiler.Typedtree;
using SR = System.Reflection;
using SCG = System.Collections.Generic;

namespace Nemerle.Completion2.Factories
{
  public class IntelliSenseModeMethodBuilder : MethodBuilder
  {
    internal new Manager          : Engine { get { base.Manager :> Engine } }
    public       TypesTreeVersion : int    { get; private set; }

    public this (par : TypeBuilder, functionAst : ClassMember.Function)
    {
      this (par, functionAst, false);
    }

    public this (par : TypeBuilder, functionAst : ClassMember.Function, is_property : bool)
    {
      base (par, functionAst, is_property);
      //_bodyTokens = functionAst.Tokens; // токены портятся при парсинга, а теперь мы парсим тела методов при построении дерева типов. Так что не запоминаем их.
      TypesTreeVersion = Manager.TypesTreeVersion;
    }

    CheckCurrentThreadIsTheAsyncWorker() : void
    {
      Nemerle.Compiler.Utils.Async.AsyncWorker.CheckCurrentThreadIsTheAsyncWorker();
    }

    //public InitializerMessages : SCG.List[CompilerMessage]
    //{
    //  get { LookupInitializerMethod().BodyMessages }
    //}
    
    public override Body : PExpr 
    {
      get { base.Body }      
      set { ResetCodeCache(); SetBodyParsed(value); }
    }
    
    public override EnsureCompiled() : void
    {
      when (TypesTreeVersion == Manager.TypesTreeVersion)
        when (IsBodyCompilable)
          _ = BodyTyped;
    }
  
    mutable _bodyMessages : SCG.List[CompilerMessage];

    public BodyMessages : SCG.List[CompilerMessage]
    {
      get
      {
        assert(IsBodyCompilable);
        when (_bodyMessages == null)
          _bodyMessages = SCG.List();
  
        _bodyMessages
      }
    }
  
    public override ResetCodeCache () : void
    {
      when (_bodyMessages != null && !_bodyTyping)
        _bodyMessages.Clear();

      unless (this.Ast.body is FunBody.Abstract)
      {
        def empty = <[ () ]>;
        this.Ast.Body       = empty;
        this.Ast.ParsedBody = FunBody.Parsed(empty);
        fun_header.Reset();
      }

      _bodyTokens     = null;
      _bodyParsed     = null;
      _bodyTyped      = null;

    }
  
    mutable _bodyTokens : Token.BracesGroup;
  
    //mutable _indent : string = "";
  
    /// The method body tokens. 
    [DebuggerBrowsable(DebuggerBrowsableState.Never)]
    public override BodyTokens : Token.BracesGroup
    {
      get
      {
        CheckCurrentThreadIsTheAsyncWorker();
        ManagerClass.Instance = Manager;
        //Trace.WriteLine($"$_indent>>>> ##### BodyTokens ($Name)!");
        assert(IsBodyCompilable);
        when (_bodyTokens == null)
        {
          //def indent = _indent; _indent += "  ";
          Manager.SetCompiletMessages(this, BodyMessages);
          _bodyTokens = PreParseMethodBody (this);
          //_indent = indent;
        }
  
        //Trace.WriteLine($"$_indent<<<< ##### BodyTokens ($Name)!");
        _bodyTokens
      }
  
      set { ResetCodeCache(); _bodyTokens = value; }
    }
    
    SetBodyParsed(mutable bodyParsed : PExpr) : void
    {
      CheckCurrentThreadIsTheAsyncWorker();
      ManagerClass.Instance = Manager;
      // Do all transform of method body.
      when (BeforeBodyTyping != null)
        foreach (handler in BeforeBodyTyping)
          bodyParsed = handler(this, bodyParsed);

      _bodyParsed = bodyParsed;

      def parsed = FunBody.Parsed(bodyParsed);
      Ast.ParsedBody = parsed;
      fun_header.body = parsed;
    }
  
    mutable _bodyParsed : PExpr;
  
    /// The method body parsed expressions. 
    [DebuggerBrowsable(DebuggerBrowsableState.Never)]
    public override BodyParsed : PExpr
    {
      get
      {
        ManagerClass.Instance = Manager;
        //Trace.WriteLine($"$_indent>>>> ##### BodyParsed ($Name)!");
        assert(IsBodyCompilable);
        when (_bodyParsed == null)
        {
          //Trace.WriteLine($">>>> ##### BodyParsed ($Name)! - _bodyParsed == null");

          when (_bodyMessages != null)
            _bodyMessages.Clear();

          // Теперь тела методв парсятся при построении дерева типов, так что если в fun_header.body
          // находится FunBody.Parsed, то берем PExpr из него.
          // if (Location.IsGenerated && !fun_header.Name.OrdinalStartsWith("_N_field_initialiser"))
          match (fun_header.body)
          {
            | FunBody.Parsed(code) => Body = code; //Trace.WriteLine($">>>> ##### BodyParsed ($Name)! - fun_header.body is FunBody.Parsed(code)");
            | _ =>
              def source = Manager.GetIdeSource(BodyLocation.FileIndex);
              def member = if (source.CompileUnit == null) null 
                           else source.CompileUnit.FindMember(BodyLocation.Line, BodyLocation.Column);

              match (member)
              {
                | Some(ClassMember.Function(body = FunBody.Parsed(expr))) => 
                  SetBodyParsed(expr);
                  //Trace.WriteLine($">>>> ##### BodyParsed ($Name)! - взяли тело из CompileUnit-а!");

                | _ =>
                  Trace.WriteLine($">>>> ##### BodyParsed ($Name)! - читаем тело из исходника! Это не здорово!");
                  //assert2(false); // VladD2: This sould not be happen!

                  //def indent = _indent; _indent += "  ";
                  Manager.SetCompiletMessages(this, BodyMessages);

                  try
                  {
                    SetBodyParsed(MainParser.ParseFunctionBody (Env, Ast.header, BodyTokens));
                  }
                  finally { Manager.SetCompiletMessages(null, null); } // reset!
                  //_indent = indent;
              }
          }
          _bodyTokens = null;
        }
  
        //Trace.WriteLine($"$_indent<<<< ##### BodyParsed ($Name)!");
        _bodyParsed
      }
    }
  
    public override IsBodyCompilable : bool
    {
      get
      {
        def res = HasAbstractBody
          || BodyLocation.FileIndex <= 0
          || Attributes %&& NemerleModifiers.Abstract
          ;
        !res
      }
    }
  
    mutable _bodyTyped  : TExpr;
    mutable _bodyTyping : bool;
  
    /// The method body parsed expressions. 
    [DebuggerBrowsable(DebuggerBrowsableState.Never)]
    public override BodyTyped : TExpr
    {
      get
      {
        CheckCurrentThreadIsTheAsyncWorker();
        _bodyTyping = true;
        ManagerClass.Instance = Manager;
        Manager.CheckSolver();
        //Trace.WriteLine($"$_indent>>>> ##### BodyTyped ($Name)!");
        assert(IsBodyCompilable);
        when (_bodyTyped == null)
        {
          //def indent = _indent; _indent += "  ";
          _ = BodyParsed; // Use side affect
          Manager.SetCompiletMessages(this, BodyMessages);
          try
          {
            //Manager.Solver.CurrentMessenger.NeedMessage = false;
            RunBodyTyper();
            when (fun_header.body != null)
              _bodyTyped = (fun_header.body :> FunBody.Typed).expr;
          }
          catch 
          { | e => 
            def bLoc = BodyLocation;
            // make location on open bracket...
            def loc = Location(bLoc.FileIndex, bLoc.Line, bLoc.Column, bLoc.Line + 1, 1);
            BodyMessages.Add(CompilerMessageForMethod(loc,
                    $"Exception:$(e.Message)", MessageKind.Error, Manager, false, this));
          }
          finally 
          {
            _bodyTyping = false;
            Manager.ReportMemberErrors(this);
            Manager.SetCompiletMessages(null, null); // reset!
          }
          //_indent = indent;
        }
  
        //Trace.WriteLine($"$_indent<<<< ##### BodyTyped ($Name)!");
        _bodyTyped
      }
    }
    
    PreParseMethodBody (method : MethodBuilder) : Token.BracesGroup
    {
      CheckCurrentThreadIsTheAsyncWorker();
      def reportError(e, msg)
      {
        Trace.WriteLine($"$msg method body of:");
        Trace.WriteLine($"\t$method");
        Trace.WriteLine( "\tfailed.");
        Trace.WriteLine($"\tError: $(e.Message)");
      }

      def loc      = method.BodyLocation;
      def engine   = Manager;
      def bodyCode =
        if (loc == Location.Default || loc.IsGenerated)
          "{}"
        else try   { engine.GetIdeSource(loc.FileIndex).GetRegion(loc); }
             catch { e => reportError(e, "Try get"); "{}" }; // return empty body!

      try
      {
        def lexer     = LexerString(engine, bodyCode, loc);
        def preparser = PreParser(lexer, method.Env);

        preparser.PreParse()
      }
      catch 
      { e =>
        reportError(e, "Try to compile");
        Token.BracesGroup(null)
      } // return empty body!
    }
  } // class
} // namespace
