using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using System.ComponentModel;
using Nemerle.Compiler;

namespace Nemerle.Statechart
{
  /// <summary>
  /// Description of FsmStateDecls.
  /// </summary>
  [Record]
  [NotifyPropertyChange]
  partial class FsmState : Located, INotifyPropertyChanged
  {

    public do_activity : list[StateAction.Do] {get;set;default []}

    [NotifyPropertyChange]
    public Super : FsmState {get;set;}
    
    public type : StateType {get;set;}
    
    // calculated
    [Memoized(Super)]
    public super_states : list[FsmState] 
    {
      get
      {
        if (Super != null) 
        {
          Super.super_states + [Super]
        } else
        {
          []
        }
      }
    }
    
    [NotifyPropertyChange]
    public sub_states : list[FsmState] {get;set;default []}
    
    public initial_tran : list[StateTransition] 
    {
      get
      {
        initial.MapDefault(_.tran, [])
      }
    }
    
    [Memoized(pseudo_states)]
    public initial : option[PseudoStateNode.Initial]
    {
      get
      {
        pseudo_states.FindType()
      }
    }
    
    [NotifyPropertyChange]
    public transitions : list[StateTransition] {get;set;default []}
    
    [NotifyPropertyChange]
    public pseudo_states : list[PseudoStateNode] {get;set;default []}
    
    public generated : bool {get;set;default false}
    
    public state : StateNode.State;

    public entry : list[StateAction.Entry] {get;set;default []}
    public exit  : list[StateAction.Exit]  {get;set;default []}

    public deferred_events : list[StateEvent] {get;set;default []}
    
    public copy_num : int {get;set;}
    
    [NotifyPropertyChange]
    public name : string {get;set;}

    public statemap : StateMap
    {
      get
      {
        analyser.statemap
      }
    }

    public cloned_from : FsmState;
    
    public analyser : FsmAnalyser;
    
    public Id : IdType { get; set; }

    [Memoized(Super)]
    public hierarchy_pos : int 
    {
      get
      {
        if (Super != null) 
        {
          Super.hierarchy_pos + 1
        } else 0
      }
    }
    
  }
}
