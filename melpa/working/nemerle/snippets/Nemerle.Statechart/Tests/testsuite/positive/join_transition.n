using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using Nemerle.Statechart;

  [statechart(<#

  flags testing auto_initial;
  //flags debug;
  
  name "join transition";
  
  state A
  {
    a => B;
  }

  region B
  {
    @(C, F): a => A;
    
    state C
    {
      b => E;
    }
    
    state E
    {
      b => C;
    }
    
  } and
  {
    
    state D
    {
      a => F;
    }
    
    state F
    {
      b => D;
    }
  }
  
  #>)]
  public class example
  {
  }

  module Test
  {
    Main() : void
    {
      def fsm = example();
      def tester = fsm.GetTester();
      tester.ConsoleTestInit();
      tester.Initiate("A", "EntryA");
      tester.Test("a", "C|D", ["A"], ["B", "C", "D"]);
      tester.Test("a", "C|F", ["D"], ["F"]);
      tester.Test("a", ["F", "C", "B"], ["A"]);
      tester.Test("a", "C|D", ["A"], ["B", "C", "D"]);
      tester.Test("b", "E|D", ["C"], ["E"]);
      tester.Test("a", "E|F", ["D"], ["F"]);
      fsm.Terminate();
    }
  }
  
 /*
BEGIN-OUTPUT
EntryA
[A]
Ok
Processing a 'a' 
ExitA
EntryB
EntryC
EntryD
[C|D]
Ok
Processing a 'a' 
ExitD
EntryF
[C|F]
Ok
Processing a 'a' 
ExitF
ExitC
ExitB
EntryA
[A]
Ok
Processing a 'a' 
ExitA
EntryB
EntryC
EntryD
[C|D]
Ok
Processing a 'b' 
ExitC
EntryE
[E|D]
Ok
Processing a 'a' 
ExitD
EntryF
[E|F]
Ok
END-OUTPUT
*/
