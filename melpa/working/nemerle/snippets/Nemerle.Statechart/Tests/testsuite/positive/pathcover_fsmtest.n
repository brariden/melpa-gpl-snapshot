using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using System.Console;
using Nemerle.Statechart;

  /// <summary>
  /// Description of PathCover.
  /// </summary>
  [statechart(<#

  flags : auto_initial
          transition_completed
          auto_entry_exit
          testing;

  name "path cover test";
  
  state A
  {
      (H*)
      g => H;
  }

  state B : A
  {
      (H*)

      d => D;
      f => @;
      k => A;
      c => E;
  }

  state C : B
  {
      a / ();
      b => E;
      m => @;
  }

  state E : B
  {
      i => D;
      j => A;
  }

  state D : A
  {
      e => B;
      n => B.H;
  }

  #>
  )]
  public class PathCoverFsm
  {

  }

  using PathCoverFsm.Event;
  /// <summary>
  /// Description of PathCoverFsmTest.
  /// </summary>
  module Test
  {

    public Main() : void
    {
      def fsm = PathCoverFsm();
      def tester = fsm.GetTester();
      tester.ConsoleTestInit(true);

      // "Starting state machine"
      tester.Initiate(["A", "B", "C"]);

      // "Testing Internal transition"
      tester.Test(A.New, null);

      // "Testing simple-to-simple transition"
      tester.Test(B.New, ["C"], ["E"]);

      // "Testing simple-to-composite transition"
      tester.Test(J.New, ["E", "B", "A"], ["A", "B", "C"]);

      // "Testing simple-to-simple, via composite"
      tester.Test(D.New, ["C", "B"], ["D"]);

      // "Testing for no match"
      tester.Test(A.New, null);

      // "Testing simple to (non-ancestor) composite"
      tester.Test(E.New, ["D"], ["B", "C"]);

      // "Testing simple-to-simple transition again"
      tester.Test(B.New, ["C"], ["E"]);

      // "Testing simple-to-simple, across ancestor"
      tester.Test(I.New, ["E", "B"], ["D"]);

      // "Testing simple-to-composite with history"
      tester.Test(G.New, ["D"], ["B", "E"]);

      // "Get back to state C"
      tester.Test(J.New, ["E", "B", "A"], ["A", "B", "C"]);

      // "Testing composite-to-composite transition"
      tester.Test(F.New, ["C", "B"], ["B", "C"]);

      // "Testing simple to sibling simple via composite transition"
      tester.Test(C.New, ["C", "B"], ["B", "E"]);

      // "Testing composite-to-composite transition"
      tester.Test(K.New, ["E", "B", "A"], ["A", "B", "C"]);

      // "Testing transition to self, but leaving the state"
      tester.Test(M.New, ["C"], ["C"]);

      // "Get to state D"
      tester.Test(D.New, ["C", "B"], ["D"]);

      // "Test going to non-ancestor composite with history (and returning to the same state)"
      tester.Test(N.New, ["D"], ["B", "C"]);

      // "Get us to state E, then D"
      tester.Test(B.New, ["C"], ["E"]);

      // "Go to state D"
      tester.Test(I.New, ["E", "B"], ["D"]);

      // "Now, test returning with history to a non-default state"
      tester.Test(N.New, ["D"], ["B", "E"]);

      fsm.Terminate();
    }

  }

/*
BEGIN-OUTPUT
EntryA
EntryB
EntryC
[C]
Ok
Processing a 'a' 
[C]
Ok
Processing a 'b' 
ExitC
EntryE
[E]
Ok
Processing a 'j' 
ExitE
ExitB
ExitA
EntryA
EntryB
EntryC
[C]
Ok
Processing a 'd' 
ExitC
ExitB
EntryD
[D]
Ok
Processing a 'a' 
[D]
Ok
Processing a 'e' 
ExitD
EntryB
EntryC
[C]
Ok
Processing a 'b' 
ExitC
EntryE
[E]
Ok
Processing a 'i' 
ExitE
ExitB
EntryD
[D]
Ok
Processing a 'g' 
ExitD
EntryB
EntryE
[E]
Ok
Processing a 'j' 
ExitE
ExitB
ExitA
EntryA
EntryB
EntryC
[C]
Ok
Processing a 'f' 
ExitC
ExitB
EntryB
EntryC
[C]
Ok
Processing a 'c' 
ExitC
ExitB
EntryB
EntryE
[E]
Ok
Processing a 'k' 
ExitE
ExitB
ExitA
EntryA
EntryB
EntryC
[C]
Ok
Processing a 'm' 
ExitC
EntryC
[C]
Ok
Processing a 'd' 
ExitC
ExitB
EntryD
[D]
Ok
Processing a 'n' 
ExitD
EntryB
EntryC
[C]
Ok
Processing a 'b' 
ExitC
EntryE
[E]
Ok
Processing a 'i' 
ExitE
ExitB
EntryD
[D]
Ok
Processing a 'n' 
ExitD
EntryB
EntryE
[E]
Ok
END-OUTPUT
*/
