using System;
using System.Collections.Generic;
using System.Linq;
using System.Windows.Forms;
using Nemerle.Statechart.Tests;
using Nemerle.Statechart;

namespace fsm_test
{

  class Test
  {
    public Name : string {get;set;}
  }        
        
  class Test2
  {
    public Name : string {get;set;}
  }
          
  class FileFsmFormControl 
  {
    form : FileFsmForm;
    fsm : FileFsm = FileFsm();
    
    public this()
    {
      form = FileFsmForm();
      init();
    }

    public Form : FileFsmForm
    {
      get
      {
        form
      }
    }

    [FormSync(form)]
    WaitingEntry() : void 
    {
      form.CaptionChange("program_name")
    }

    [FormSync(form)]
    NewEntry() : void 
    {
      form.CaptionChange("program_name - [new document]")
    }

    [FormSync(form)]
    ChangedEntry() : void 
    {
      form.CaptionChange("program_name - [document]*")
    }

    [FormSync(form)]
    SavedEntry() : void 
    {
      form.CaptionChange("program_name - [document]")
    }

    mutable exit : bool = false;
    
    [FormSync(form)]
    ForceClose() : void
    {
      exit = true;
      form.Close();
    }
    
    Closing(_ : object, evt : FormClosingEventArgs) : void
    {
      when (!exit)
      {
        fsm.exit_pressed();
        evt.Cancel = true;
      }
    }
    
    init() : void 
    {
      Form.NewHandler         += _ => fsm.new_pressed();
      Form.OpenHandler        += _ => fsm.open_pressed();
      Form.SaveHandler        += _ => fsm.save_pressed();
      Form.ChangeHandler      += _ => fsm.Change();
      Form.SaveAsHandler      += _ => fsm.save_as_pressed();
      Form.FormClosing        += Closing;
      fsm.Terminated          += _ => ForceClose();
      fsm.sync                = form;
      fsm.SaveEna             += form.SaveEna;
      fsm.SaveDis             += form.SaveDis;
      fsm.SaveAsEna           += form.SaveAsEna;
      fsm.SaveAllDis          += form.SaveAllDis;
      fsm.EntrySaved          += SavedEntry;
      fsm.EntryWaiting        += WaitingEntry;
      fsm.EntryNew            += NewEntry;
      fsm.EntryChanged        += ChangedEntry;
      fsm.SaveDialog          = Form.SaveDlg;
      fsm.OpenDialog          = Form.OpenDlg;
      fsm.PropertyChanged     += _ => UpdateState();
      fsm.NewDialog = _ => MessageBox.Show("New file", "New file", MessageBoxButtons.OKCancel) == DialogResult.OK;
      fsm.OpenFileAction = file => MessageBox.Show($"Opening file $file", "Opening file", MessageBoxButtons.OKCancel) == DialogResult.OK;
      fsm.SaveFileAction = file => MessageBox.Show($"File $file saved", "Saving file", MessageBoxButtons.OKCancel) == DialogResult.OK;
      fsm.Initiate();
      UpdateState()
    }

    UpdateState() : void 
    {
      Form.SetState(if (fsm.IsTerminated) "Terminated" else fsm.ToString(), fsm.LastSaved)
    }

  }

  module Program 
  {
    public mutable form : FileFsmFormControl;

    /// <summary>
    /// The main entry point for the application.
    /// </summary>
    [STAThread()]
    Main() : void 
    {
      Application.EnableVisualStyles();
      Application.SetCompatibleTextRenderingDefault(false);
      form = FileFsmFormControl();
      Application.Run(form.Form)
    }
  }


}
