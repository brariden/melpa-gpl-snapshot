;;;;;
;;;;;
;;;;; Term Rewriting
;;;;;
;;;;;

;(set-term-rewriting-rule {
;                          [(+ (rtu $n) (rtu ,n)^2 ... (rtu ,n)^,n) -1]
;                          [(+ w w^2) -1]
;                          [(* (sqrt $x) (sqrt $y)) (* (gcd x y) (sqrt (/ (* x y) (gcd x y)^2)))]
;                          [(rt $n $x)^,n x]
;                          [(sqrt $x)^2 x]
;                          [(rtu $n)^,n 1]
;                          [w^3 1]
;                          [i^2 -1]
;                          })

(define $math-normalize
  (lambda [$mexpr]
    (if (tensor? mexpr)
      (tensor-map math-normalize mexpr)
      (if (scalar? mexpr)
        (if (rational? mexpr)
          mexpr
          (map-polys rewrite-rule-rtu-plus
                     (map-polys rewrite-rule-w-plus
                                (map-terms rewrite-rule-rt
                                           (map-terms rewrite-rule-sqrt
                                                      (map-terms rewrite-rule-rtu
                                                                 (map-terms rewrite-rule-w
                                                                            (map-terms rewrite-rule-i
                                                                                       mexpr))))))))
        mexpr))))

(define $rewrite-rule-rtu-plus
  (lambda [$poly]
    (match poly math-expr
      {[<plus <cons <mult $a <ncons $k (,rtu (& ,(+ k 1) $n)) $mr>>
                    (loop $i [2 ,(- n 1)]
                      <cons <mult ,a <ncons ,(- n i) ,(rtu n) ,mr>> ...>
                      $pr)>>
        (rewrite-rule-rtu-plus (+' pr (*' -1 a mr)))]
       [_ poly]})))

(define $rewrite-rule-w-plus
  (lambda [$poly]
    (match poly math-expr
      {[<plus <cons <mult $a <ncons ,2 ,w $mr>>
                    <cons <mult $b <ncons ,1 ,w ,mr>>
                          $pr>>>
        (rewrite-rule-w-plus (+' pr
                                 (*' -1 a mr)
                                 (*' (- b a) w mr)
                                 ))]
       [_ poly]})))

(define $rewrite-rule-i
  (lambda [$term]
    (match term math-expr
      {
       [<mult $a <ncons (& ?even? $k) ,i $r>>
        (*' a (**' -1 (quotient k 2)) r)]
       [<mult $a <ncons $k ,i $r>>
        (*' a (**' -1 (quotient k 2)) i r)]
       [_ term]})))

(define $rewrite-rule-w
  (lambda [$term]
    (match term math-expr
      {[<mult $a <ncons (& ?(gte? $ 3) $k) ,w $r>>
        (*' a (**' w (remainder k 3)) r)]
       [_ term]})))

(define $rewrite-rule-rtu
  (lambda [$term]
    (match term math-expr
      {[<mult $a <ncons $k (,rtu (& ?(gte? k $) $n)) $r>>
        (*' a (**' (rtu n) (remainder k n)) r)]
       [_ term]})))

(define $rewrite-rule-sqrt
  (lambda [$term]
    (match term math-expr
      {[<mult $a <cons (,sqrt $x) <cons (,sqrt ,x) $r>>>
        (rewrite-rule-sqrt (*' a x r))]
       [<mult $a <cons (,sqrt (& ?term? $x)) <cons (,sqrt (& ?term? $y)) $r>>>
        (let* {[$d (gcd x y)]
               [[$a1 $x1] (from-monomial (/ x d))]
               [[$a2 $y1] (from-monomial (/ y d))]}
          (*' a d
             (sqrt (*' a1 a2)) (sqrt x1) (sqrt y1)
             r))]
       [_ term]})))

(define $rewrite-rule-rt
  (lambda [$term]
    (match term math-expr
      {[<mult $a <ncons $k (,rt (& ?(gte? k $) $n) $x) $r>>
        (*' a (**' x (quotient k n)) (**' (rt n x) (remainder k n)) r)]
       [_ term]})))
