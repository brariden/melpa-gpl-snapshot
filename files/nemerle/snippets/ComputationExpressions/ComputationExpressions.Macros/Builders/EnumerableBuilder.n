/*
 * Copyright (c) 2010  David Sorokin <david.sorokin@gmail.com>
 * Copyright (c) 2010  rampelstinskin@gmail.com
 * All rights reserved.
 *
 * Redistribution and use in source and binary forms, with or without
 * modification, are permitted provided that the following conditions
 * are met:
 *    1. Redistributions of source code must retain the above copyright
 *       notice, this list of conditions and the following disclaimer.
 *    2. Redistributions in binary form must reproduce the above copyright
 *       notice, this list of conditions and the following disclaimer in the
 *       documentation and/or other materials provided with the distribution.
 * 
 * THIS SOFTWARE IS PROVIDED BY THE AUTHOR ``AS IS'' AND ANY EXPRESS OR
 * IMPLIED WARRANTIES, INCLUDING, BUT NOT LIMITED TO, THE IMPLIED WARRANTIES
 * OF MERCHANTABILITY AND FITNESS FOR A PARTICULAR PURPOSE ARE DISCLAIMED. IN
 * NO EVENT SHALL THE AUTHOR BE LIABLE FOR ANY DIRECT, INDIRECT, INCIDENTAL,
 * SPECIAL, EXEMPLARY, OR CONSEQUENTIAL DAMAGES (INCLUDING, BUT NOT LIMITED
 * TO, PROCUREMENT OF SUBSTITUTE GOODS OR SERVICES; LOSS OF USE, DATA, OR
 * PROFITS; OR BUSINESS INTERRUPTION) HOWEVER CAUSED AND ON ANY THEORY OF
 * LIABILITY, WHETHER IN CONTRACT, STRICT LIABILITY, OR TORT (INCLUDING
 * NEGLIGENCE OR OTHERWISE) ARISING IN ANY WAY OUT OF THE USE OF THIS
 * SOFTWARE, EVEN IF ADVISED OF THE POSSIBILITY OF SUCH DAMAGE.
 */

using Nemerle;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;

using System;
using System.Collections.Generic;

namespace Nemerle.ComputationExpressions.Extensions
{
  using Nemerle.ComputationExpressions.Internal;

  /// <summary>
  /// Defines a computation builder for the enumerable comprehension.
  /// </summary>
  internal class EnumerableBuilder : ComputationBuilder
  {
    static internal Instance : EnumerableBuilder = EnumerableBuilder ();
    
    public override Return (expr : PExpr) : PExpr
    {
      Message.FatalError (expr.Location, "The enumerable comprehension doesn't support the return construct. Use yield instead.")
    }
    
    public override ReturnComp (expr : PExpr) : PExpr
    {
      Message.FatalError (expr.Location, "The enumerable comprehension doesn't support the returncomp construct. Use yieldcomp instead.")
    }
    
    public override Yield (expr : PExpr) : PExpr
    {
      <[ EnumerableHelper.Singleton ($expr) ]>
    }
    
    public override YieldComp (expr : PExpr) : PExpr
    {
      expr
    }
    
    public override Bind (expr : PExpr, name : PExpr, cont : PExpr) : PExpr
    {
      ignore (name);
      ignore (cont);
      
      Message.FatalError (expr.Location, "The enumerable comprehension doesn't support the defcomp construct. Use yield and yieldcomp instead.")
    }
    
    public override Delay (expr : PExpr) : PExpr
    {
      Message.FatalError (expr.Location, "The enumerable comprehension doesn't support the Delay method.")
    }
    
    public override Combine (expr1 : PExpr, expr2 : PExpr) : PExpr 
    {
      <[ EnumerableHelper.Append ($expr1, EnumerableHelper.Delay (() => $expr2)) ]>
    }
    
    public override Run (expr : PExpr) : PExpr
    {
      <[ EnumerableHelper.Delay (() => $expr) ]>
    }
    
    public override Zero () : PExpr
    {
      <[ EnumerableHelper.Empty () ]>
    }
    
    public override While (pred : PExpr, body : PExpr) : PExpr
    {
      <[ EnumerableHelper.While (() => $pred, EnumerableHelper.Delay (() => $body)) ]>
    }
  
    protected override ForEach (coll : PExpr, cont : PExpr) : PExpr 
    {
      <[ EnumerableHelper.ForEach ($coll, $cont) ]>
    }
    
    public override TryFinally (body : PExpr, handler : PExpr) : PExpr
    {
      <[ EnumerableHelper.TryFinally (EnumerableHelper.Delay (() => $body), () => $handler) ]>
    }
    
    protected override TryCatch (body : PExpr, cont : PExpr) : PExpr
    {
      <[ EnumerableHelper.TryCatch (EnumerableHelper.Delay (() => $body), $cont) ]>
    }
    
    public override Using (expr : PExpr, name : PExpr, body : PExpr) : PExpr
    {
      <[ EnumerableHelper.Using ($expr, $name => $body) ]>
    }
  }
}
