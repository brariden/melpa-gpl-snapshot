namespace Nemerle.ComputationExpressions.Async
{
  [Record]
  internal class AsyncWhile : Async[FakeVoid]
  {
    private pred : void -> bool;
    private body : void -> Async[FakeVoid];

    [RecordIgnore]
    private mutable curBody : Async[FakeVoid] = null;

    public override Exec(job : AsyncJobBase, res : ref FakeVoid) : Async[FakeVoid]
    {
      if (curBody == null)
      {
        if (pred())
        {
          curBody = job.ExecWhileReady(body(), ref res);
          this;
        }
        else
        {
          null;
        }
      }
      else
      {
        curBody = job.ExecWhileReady(curBody, ref res);
        this;
      }
    }

    public override Cancel() : void
    {
      when (curBody != null)
        curBody.Cancel();
    }
  }
}
