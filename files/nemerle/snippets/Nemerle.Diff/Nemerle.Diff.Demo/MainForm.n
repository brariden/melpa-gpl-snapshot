using Nemerle.Diff;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.ComponentModel;
using System.Data;
using System.Drawing;
using System.IO;
using System.Linq;
using System.Text;
using System.Windows.Forms;

namespace Demo
{
    /// <summary>
    /// Description of MainForm.
    /// </summary>
    public partial class MainForm : Form
    {
        public this()
        {
            InitializeComponent();
        }

        private toolStripButton_Click (_sender : object,  _e : System.EventArgs) : void
        {
            openFileDialog.Title = "Select A-version of text file";
            when (openFileDialog.ShowDialog() == DialogResult.OK)
            {
                def textA = File.ReadAllLines(openFileDialog.FileName);
                openFileDialog.Title = "Select B-version of text file";
                when (openFileDialog.ShowDialog() == DialogResult.OK)
                {
                    def textB = File.ReadAllLines(openFileDialog.FileName);
                    def diffData = textA.Diff(textB);
                    def htmlBuilder = StringBuilder();
                    _ = htmlBuilder.Append("<!DOCTYPE HTML PUBLIC \"-//W3C//DTD HTML 4.01 Transitional//EN\" \"http://www.w3.org/TR/html4/loose.dtd\"><html><head><meta http-equiv=\"Content-Type\" content=\"text/html; charset=utf-8\"><title>Nemerle.Diff Demo</title></head><body><pre>");
                    mutable indexA = 0;
                    foreach(diffItem in diffData)
                    {
                        while (indexA < diffItem.Index)
                        {
                            _ = htmlBuilder.Append($"<span>$(textA[indexA])</span><br>");
                            indexA++;
                        }
                        for (mutable i = indexA; i < indexA + diffItem.Deleted; i++)
                            _ = htmlBuilder.Append($"<span style='background-color: rgb(255,127,127);'>$(textA[i])</span><br>");
                        indexA += diffItem.Deleted;
                        foreach (insertedItem in diffItem.Inserted)
                            _ = htmlBuilder.Append($"<span style='background-color: rgb(127,255,127);'>$(insertedItem)</span><br>");
                    }
                    while (indexA < textA.Length)
                    {
                        _ = htmlBuilder.Append($"<span>$(textA[indexA])</span><br>");
                        indexA++;
                    }
                    _ = htmlBuilder.Append("</pre></body></html>");
                    def htmlFileName = $"$(Environment.GetEnvironmentVariable(\"TEMP\"))$(Path.DirectorySeparatorChar)Nemerle.Diff_Demo.html";
                    File.WriteAllText(htmlFileName, htmlBuilder.ToString());
                    webBrowser.Navigate(htmlFileName);
                }
            }
        }
    }
}
