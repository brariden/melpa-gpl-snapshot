using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using Nemerle.Statechart;

using System.Collections.Generic;
using System.Linq;

  /// <summary>
  /// Description of example.
  /// </summary>
  [statechart(
  <#
  flags testing;
  
  state A
  {
    a => B.en;
  }
  
  state B
  {
    $> / e1;
    
    (en) / a1 => {
                   g1   / a2 => C;
                   else / a3 => D;
                 }
           
    a => A;
    
    state C
    {
      entry / e2;
    }
    
    state D
    {
      entry / e3;
    }
  }
  
  #>)]
  public class example
  {
    public g1 : bool {get;set;}
  }

  module Test
  {
    Main() : void
    {
      def fsm = example();
      def tester = fsm.GetTester();
      tester.ConsoleTestInit();
      tester.Initiate("A", "EntryA");
      tester.Test("a", "D", "ExitA", "e1", "EntryB", "a1", "a3", "e3", "EntryD");
      tester.Test("a", "A", "ExitD", "ExitB", "EntryA");
      fsm.g1 = true;
      tester.Test("a", "C", "ExitA", "e1", "EntryB", "a1", "a2", "e2", "EntryC");
      tester.Test("a", "A", "ExitC", "ExitB", "EntryA");
      fsm.Terminate();
    }
  }
  
/*
BEGIN-OUTPUT
EntryA
[A]
Ok
Processing a 'a' 
ExitA
e1
EntryB
a1
a3
e3
EntryD
[D]
Ok
Processing a 'a' 
ExitD
ExitB
EntryA
[A]
Ok
Processing a 'a' 
ExitA
e1
EntryB
a1
a2
e2
EntryC
[C]
Ok
Processing a 'a' 
ExitC
ExitB
EntryA
[A]
Ok
END-OUTPUT
*/
