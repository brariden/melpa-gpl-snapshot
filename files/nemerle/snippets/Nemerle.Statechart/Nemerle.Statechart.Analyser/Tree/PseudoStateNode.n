using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using Nemerle.Compiler;

namespace Nemerle.Statechart
{
  using FsmState;
  using ListUtils;


  [Record]
  variant PseudoStateNode : Located, IEquatable[PseudoStateNode]
  {
    | Initial 
      { 
        tran : list[StateTransition] {get;set;default []}

        this(st : FsmState, tran : list[StateNode.Transition])
        {
          base(0, st);
          this.tran = tran.Map(StateTransition(this, TransitionEvent.Completion(-1), _));
          this.tran.Iter(x => x.kind = TransitionKind.Local);
        }

        this(st : FsmState, tostate : FsmState)
        {
          base(0, st);
          this.tran = StateTransition(this, tostate, TransitionKind.Local) :: []
        }
      }
    | History 
      {
        public this(st : FsmState, hist : PseudoStateModelNode.History)
        {
          base(0, st);
          type = hist.type;
          default = hist.default.Map(x => StateTransition(st, TransitionEvent.Completion(-1), x))
        }

        public SimpleName : string
        {
          get
          {
            $"$(state)History"
          }
        }        

        public default : option[StateTransition] {get;set;}

        public type : HistoryType;
      }
    | Junction
      {
        name : string;
        tree : FsmJunctionNode;
        type : JunctionType;

        decision_tree : DecisionNode {get;set;}
        IsUnnamed : bool;

        public static GetUnnamedName(this an : FsmAnalyser, type : JunctionType) : string
        {
          match (type) 
          {
            | JunctionType.Choice   => def num = an.NextChoiceNum();
                                      $"UnnamedChoice$num"
            | JunctionType.Junction => def num = an.NextJunctionNum();
                                      $"UnnamedJunction$num"
            | _                     => throw NotImplementedException()
          }
        }

        this(st : FsmState, node : PseudoStateModelNode.Junction)
        {
          base(0, st);
          def an = st.analyser;
          name = match (node) 
                  {
                    | PseudoStateModelNode.Junction(name = null, type = t) => IsUnnamed = true;
                                                                              GetUnnamedName(an, t);
                    | Junction(name = n)                                   => n
                  }

          tree = FsmJunctionNode.Create(this, node.nodes);
          type = node.type;
        }

        override ToString() : string
        {
          Name
        }

      }

    | Fork 
      {
        target    : list[TransitionTarget] {get;set;}
        //fork_tran : list[StateTransition] {get;set;default []}
      }
    | Join 
      {
        name : string;
        joined_states : list[FsmState] {get;set;default []}
        transition : StateTransition {get;set;}

        this(st : FsmState, node : PseudoStateModelNode.Join)
        {
          base(0, st);
          name = node.name;
          transition = StateTransition(this, null, TransitionKind.External);
          joined_states = node.sources.Map(x => FsmState(x.FullName, st.analyser, StateType.State()));
          def tran = node.tran;
          transition.To = TransitionTarget.Create(st, tran.to);
          transition.guard = tran.guard;
          transition.action = tran.action.MapDefault(x => x.actions, []);
        }
      }
    | EntryPoint 
      { 
        name : string; 
        tran : StateTransition;

        this(st : FsmState, node : PseudoStateModelNode.EntryPoint)
        {
          base(0, st);
          tran = if (node.tran != null) StateTransition(this, TransitionEvent.Completion(-1), node.tran) else null;
          this.name = node.name;
        }
      }

    | ExitPoint 
      { 
        name : string; 
        tran : option[StateTransition] {get;set;default None()} 

        this(st : FsmState, node : PseudoStateModelNode.ExitPoint)
        {
          base(0, st);
          tran = node.tran.Map(StateTransition(this, TransitionEvent.Completion(-1), _));
          this.name = node.name;
        }
      }

    | Terminate

    public static Create(st : FsmState, pseudo : PseudoStateModelNode) : PseudoStateNode
    {
      def npseudo = match (pseudo)
                     {
                       | Initial(tr)                                => Initial(st, tr)
                       | History as hist                            => History(st, hist)
                       | Junction as j                              => Junction(st, j)
                       | EntryPoint as e                            => EntryPoint(st, e)
                       | ExitPoint as e                             => ExitPoint(st, e)
                       | Terminate                                  => Terminate(0, st)
                       | Fork(tar)                                  => Fork(0, st, tar.Map(x => TransitionTarget.Create(st, x)))
                       | Join as e                                  => Join(st, e)
                     }
      npseudo.Location = pseudo.Location;
      npseudo
    }

    public Actions : list[FsmAction]
    {
      get
      {
        match (this)
        {
          | Initial(tran = tr)         => tr.SelectMany(_.action).NToList()
          | History(default = Some(tr))
          | Join(transition = tr)
          | EntryPoint(tran = tr) 
          | ExitPoint(tran = Some(tr)) => tr.action
          | Junction(tree = t)         => t.GetBreadthFirstEnumerable().SelectMany(_.actions).NToList()
          | _                          => []
        }
      }
    }
    
    public Clone() : PseudoStateNode
    {
      def change_pseudo(tr, p)
      {
        tr.FromPseudo = Some(p)
      }
      
      match (this)
      {
        | EntryPoint(n, tr)             => def p = EntryPoint(Location, copy, state, n, tr.Clone());
                                           change_pseudo(p.tran, p);
                                           p
        | ExitPoint(n) as p             => def p = ExitPoint(Location, copy, state, n, p.tran.Map(_.Clone()));
                                           p.tran.Iter(x => change_pseudo(x, p));
                                           p
        | Fork as p                     => Fork(Location, copy, state, p.target) 
        | History(t) as p               => def p = History(Location, copy, state, p.default.Map(x => x.Clone()), t);
                                           p.default.Iter(x => change_pseudo(x, p));
                                           p
        | Initial as p                  => def p = Initial(Location, copy, state, p.tran.Map(_.Clone()));
                                           p.tran.Iter(x => change_pseudo(x, p));
                                           p
        | Join(j) as p                  => def p = Join(Location, copy, state, j, p.joined_states, p.transition.Clone());
                                           change_pseudo(p.transition, p);
                                           p
        | Junction(n, t, ty, un) as p   => def p = Junction(Location, copy, state, n, t.Clone(), ty, p.decision_tree, un);
                                           p.tree.ParentNode = p;
                                           p
        | Terminate                     => Terminate(Location, copy, state)
      }
    }

    [Memoized]
    public NestedTransitions : list[StateTransition]
    {
      get
      {
        match (this)
        {
          | Initial(tran = tr)                => tr
          | EntryPoint(_, tr) 
          | Join(transition = tr)
          | ExitPoint(tran = Some(tr))        => tr :: []
          | Fork(target = t)                  => t.SelectMany(_.NestedTransitions).NToList()
          | Junction(tree = t)                => t.Targets.SelectMany(_.NestedTransitions).NToList()
          | _                                 => []
        }
      }
    }

    copy_name : string
    {
      get
      {
        if (copy == 0) "" else $"$copy"
      }
    }

    public Retarget(find_state : FsmState -> FsmState, find_pseudo : PseudoStateNode -> PseudoStateNode) : void
    {
      match (this)
      {
        | Initial as p                  => p.tran.Iter(x => x.Retarget(find_state, find_pseudo))
        | EntryPoint(_, tr)
        | Join(transition = tr)
        | History(default = Some(tr))
        | ExitPoint(tran = Some(tr))    => tr.Retarget(find_state, find_pseudo)
        | Fork(target = t) as p         => p.target = t.Map(x => x.Retarget(find_state, find_pseudo));
        | Junction(tree = t)            => foreach (t in t.GetBreadthFirstEnumerable())
                                           {
                                             | FsmJunctionNode.Target(target = t) as p => p.target = t.Retarget(find_state, find_pseudo)
                                             | _                                       => ()
                                           }
        | _                             => ()
      }
    }

    public copy : int {get;set;default 0}

    public state : FsmState {get;set;}

    public NativeName : option[string]
    {
      get
      {
        match (this)
        {
          | Join(name = null)   => None()
          | Join(name = n)
          | Junction(name = n, IsUnnamed = false) 
          | EntryPoint(name = n)
          | ExitPoint(name = n) => Some(n)
          | _                   => None()
        }
      }
    }

    public Equals(b : PseudoStateNode) : bool
    {
      this : object == b
    }

    public override GetHashCode() : int
    {
      Name.GetHashCode()
    }

    public Name : string
    {
      get
      {
        def name = match (this)
                    {
                      | PseudoStateNode.History(type = t) => 

              def type = match (t)
                          {
                            | HistoryType.Deep     => "Deep"
                            | Shallow              => "Shallow"
                            | None                 => ""
                          }

              if (state != null)
              {
                if (state.history.IsOne) $"$(state)History" else $"$(state)$(type)History"
              } else "History";
                      | Join(name = null) as j    => $<#Join..$(j.joined_states;"_")To$(j.transition.To)#>//$copy_name
                      | Join(name = n)            => $"Join$n"//$copy_name
                      | PseudoStateNode.Terminate => $"$(state)Terminate"
                      | Fork(target = t)          => $<#Fork$(state)To..$(t;"_")#>
                      | Initial                   => $"$(state)Initial"
                      | Junction(name = n)        => $"$(n)At$state"
                      | EntryPoint(name = n)      => $"EntryPoint$n"
                      | ExitPoint(name = n)       => $"ExitPoint$n"
                    }
        name + copy_name
      }
    }

    [Memoized]
    public ToHPseudo : HPseudoState
    {
      get
      {
        def st = state?.ToHState;
        match (this)
        {
          | Fork(target = t)    => HPseudoState.Fork(st, t.Map(x => x.ToState.ToHState))
          | History as p        => HPseudoState.History(st, p.default.Map(x => x.ToHTransition), p.type)
          | Join as p           => HPseudoState.Join(st, p.name, p.joined_states.Map(x => x.ToHState), null)
          | Junction as p       => HPseudoState.Junction(st, p.name, p.tree.ToHJunctionNode, p.type, null)
          | Terminate           => HPseudoState.Terminate(st)
          | _                   => null
        }
      }
    }

    public override ToString() : string
    {
      Name
    }

  }


}
