using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;

namespace Nemerle.Statechart
{
  [Record]
  class GenPath
  {
    public map  : Map[string, string];
    public node : PathNode;
    public gen : StaticGenerator;

    action(name : string, pars : list[string] = null) : FsmAction
    {
      def pars = pars ?? [];
      FsmAction.Normal(-1, name, pars)
    }

    [Memoized]
    public action_name : FsmAction
    {
      get
      {
        match (node)
        {
          | ClearHistory(hist)  => def name = hist.Head.state;
                                   action(HistoryEmitter.ClearActionName(name))
          | SaveHistory(hist)   => def name = hist.Head.state;
                                   action(HistoryEmitter.SaveActionName(name))
          | Action(act)         => act
          | Entry(action = act) => act
          | Exit(action = act)  => act
          | RunDoActivity(act)  => FsmAction.Expr(GenActivity.StartCode(act))
          | StopDoActivity(act) => FsmAction.Expr(GenActivity.StopCode(act))
        }
      }
    }

    [Memoized]
    public node_name : FsmAction
    {
      get
      {
        match (node)
        {
          | Entry(state = st)                         => action($"Entry$st")
          | Exit(state = st)                          => action($"Exit$st")
          | _                                         => action_name
        }
      }
    }

    [Memoized]
    public gen_action : GenAction
    {
      get
      {
        GenAction(action_name, map, gen)
      }
    }

    [Memoized]
    public node_action : GenAction
    {
      get
      {
        GenAction(node_name, map, gen)
      }
    }
  

    public ExecExpr : PExpr
    {
      get
      {
        match (node)
        {
          | Action
          | Entry                         
          | Exit       => gen_action.ExecExpr
          | _          => gen_action.DeclExecExpr
        }
      }
    }

    public TranExecExpr : PExpr
    {
      get
      {
        match (node)
        {
          | Action
          | Entry                         
          | Exit       => node_action.ExecExpr
          | _          => gen_action.DeclExecExpr
        }

      }
    }

    public BodyFunc : PExpr -> PExpr {get;set;}

    public Define() : void
    {
      def act = node_action;
      act.BodyFunc = BodyFunc;
      act.Define()
    }
  }

  [Record]
  class GenPathNode
  {
    map  : Map[string, string];
    gen : StaticGenerator;
    flags : MachineFlags;
    
    [Accessor]
    path : TranPath;

    kind : PathKind
    {
      get
      {
        path.kind
      }
    }

    [Memoized]
    node : list[GenPath]
    {
      get
      {
        match (kind)
        {
          | Entry  => GenPath(map, PathNode.Entry(state, FsmAction.Empty), gen, x => x) :: []
          | Exit   => GenPath(map, PathNode.Exit(state, FsmAction.Empty), gen, x => x) :: []
          | Action => path.nodes.Map(GenPath(map, _, gen, x => x))
          | _      => null
        }
      }
    }

    state : HState
    {
      get
      {
        path.state
      }
    }

    [Memoized]
    path_nodes  : list[GenPath]
    {
      get
      {
        path.nodes.Map(x => GenPath(map, x, gen, x => x))
      }
    }

    [Memoized]
    TranPathNodes : list[GenPath]
    {
      get
      {
        def matched(n)
        {
          match (n.node)
          {
            | PathNode.Entry 
            | Exit
            | Action => true
            | _      => false
          }
        }

        path_nodes.Filter(matched)
      }
    }

    [Memoized]
    public ExecExpr : PExpr
    {
      get
      {
        def expr = TranPathNodes.Map(x => x.TranExecExpr);
        <[ {..$expr} ]>
      }
    }

    [Memoized]
    public HandlerExecExpr : PExpr
    {
      get
      {
        def expr = path_nodes.Map(x => x.ExecExpr);
        <[ {..$expr} ]>
      }
    }

    [Memoized]
    public NodeExecExpr : PExpr
    {
      get
      {
        def expr = node.Map(x => x.TranExecExpr);
        <[ {..$expr} ]>
      }
    }

    intBodyFunc(n : GenPath, x : PExpr) : PExpr
    {
      def add = if (flags.TestingFeatures) <[ OnActionExecuted($(n.node_action.DeclName)) ]> else <[]>;
      match (kind)
      {
        | Entry => <[ $x; $HandlerExecExpr; $add ]>
        | Exit  => <[ $HandlerExecExpr; $x; $add; ]>
        | _     => x
      }
    }

    public IsEmpty : bool
    {
      get
      {
        path.IsEmpty
      }
    }

    public Define() : void
    {
      foreach (n in node)
      {
        n.BodyFunc = intBodyFunc(n, _);
        n.Define()
      }
    }
  }

  [Record]
  class GenTranPath
  {
    map  : Map[string, string];
    gen : StaticGenerator;
    flags : MachineFlags;
    
    tran_path : list[TranPath];

    public this(seq : PathSequence, map : Map[string, string], gen : StaticGenerator, flags : MachineFlags)
    {
      tran_path = seq.TranPath.NToList();
      this.map = map;
      this.gen = gen;
      this.flags = flags;
    }

    public this(seq : list[TranPath], map : Map[string, string], gen : StaticGenerator, flags : MachineFlags)
    {
      tran_path = seq;
      this.map = map;
      this.gen = gen;
      this.flags = flags;
    }

    [Memoized]
    public tran_act_nodes : list[GenPathNode]
    {
      get
      {
        path.Filter(x => x.Path.kind == PathKind.Action)
      }
    }

    [Memoized]
    public path   : list[GenPathNode]
    {
      get
      {
        tran_path.Map(GenPathNode(map, gen, flags, _))
      }
    }

    [Memoized]
    public TranBody : PExpr
    {
      get
      {
        def path = if (flags.AutoEntryExitActions) path else path.Filter(x => !x.IsEmpty);
        def body = path.Map(x => x.NodeExecExpr);
        <[ {..$body } ]>;
      }
    }

    public DefineHandlers() : void
    {
      foreach (act in path)
      {
        if (!flags.AutoEntryExitActions && act.IsEmpty) () else act.Define()
      }
    }
  }
}
