using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;

namespace Nemerle.Statechart
{

  [Record]
  class SingleHistoryEmit
  {
    gen     : StaticGenerator;
    [Accessor]
    history : PseudoState.History;
    
    [Memoized]
    public name_at_state : string
    {
      get
      {
        NameAtState(history)
      }
    }

    public static NameAtState(h : PseudoState.History) : string
    {
      $"$(h.state)History"
    }
    
    public name : string
    {
      get
      {
        history.Name
      }
    }

    [Memoized]
    restore_name : string
    {
      get
      {
        get_restore_name(history)
      }
    }
  
    static get_restore_name(h : PseudoState.History) : string
    {
      $"$(h.Name)Restore"
    }
    
    [Memoized]
    restore_func : option[ClassMember]
    {
      get
      {
        if (restore_body is Some(r)) Some(<[decl: $(restore_name : usesite)() : State { $r } ]>) else None()
      }
    }
    
    
    [Memoized]
    default_tran_name : option[string]
    {
      get
      {
        history.default.Map(_ => $"$(name)DefaultTransition")
      }
    }
    

    
    [Memoized]
    restore_func_at_state : ClassMember
    {
      get
      {
        <[decl: public virtual $(history_restore_name_at_state : usesite)(_ : $(gen.ChartName : usesite)) : State { null } ]>
      }
    }
    
    get_history_restore_handler(path : HistoryPath) : ClassMember
    {
      <[decl: public override $(history_restore_name_at_state : usesite)(fsm : $(gen.ChartName : usesite)) : State { fsm.$(path.Name : usesite)() }]>
    }
    
    [Memoized]
    restore_body : option[PExpr]
    {
      get
      {
        if (default_tran_name is Some(n))
        {
          def body = <[ if ($(name_at_state : usesite) != null) // check if
                            $(name_at_state : usesite).$(history_restore_name_at_state : usesite)(this); else $(n : usesite)(); ]>;
          Some(body)
        } else None()
      }
    }
    
    [Memoized]
    default_transition : option[ClassMember]
    {
      get
      {
        history.default.Map(x => gen.GetTransitionFunction(default_tran_name.Value, x))
      }
    }

    [Memoized]
    history_restore_name_at_state : string
    {
      get
      {
        get_history_restore_name_at_state(history)
      }
    }
    
    static get_history_restore_name_at_state(h : PseudoState.History) : string
    {
      | PseudoState.History(type = t) when (h.state.history.IsTwo) => $"$(t)HistoryFrom$(h.state)"
      | _                                                          => $"HistoryFrom$(h.state)"
    }
    
    public static HistoryRestoreCode(hist : PseudoState.History) : PExpr
    {
      if (hist.default.IsSome)
      {
        def name = get_restore_name(hist);
        <[ $(name : usesite)();]>
      } else
      {
        def n    = hist.Name;
        def name = get_history_restore_name_at_state(hist);
        <[ $(n : usesite).$(name : usesite)(this) ]>;
      }
    }
    
    public Emit() : void
    {
      gen.state_builder.Define(restore_func_at_state); // history restore definitions in top state
      def defs = if (restore_func is Some(f)) f :: default_transition.Value :: [] else [];
      gen.Define(defs); // add definition to state class

      // history restore definitions in state
      foreach ((st, ty) in gen.StateBuilders)
      {
        def paths = st.HistoryPath;
        match (paths.Find(x => x.History == history : object))
        {
          | Some(h) => gen.Define(ty, get_history_restore_handler(h))
          | _       => ()
        }
           
      }
    }
    
  }
  
  [Record]
  class HistoryEmitter
  {
    history : list[SingleHistoryEmit];
    
    gen     : StaticGenerator;
    
    name_at_state : string
    {
      get
      {
        history.Head.name_at_state
      }
    }

    public static SaveActionName(state : HState) : string
    {
      $"$(state)HistorySave"
    }
    
    [Memoized]
    save_func_name : string
    {
      get
      {
        SaveActionName(state)
      }
    }

    [Memoized]
    save_func : ClassMember
    {
      get
      {
        <[
          decl: $(save_func_name : usesite)() : void 
          { 
            $(name_at_state : usesite) = cur_state;
          }
        ]>
      }
    }

    public static ClearActionName(state : HState) : string
    {
      $"$(state)HistoryClear"
    }
    
    [Memoized]
    clear_func_name : string
    {
      get
      {
        ClearActionName(state)
      }
    }
    
    [Memoized]
    clear_func : ClassMember
    {
      get
      {
        <[
          decl: $(clear_func_name : usesite)() : void 
          {
              $(name_at_state : usesite) = null
          }
        ]>
      }
    }
    
    
    [Memoized]
    history_field : ClassMember
    {
      get
      {
        <[ decl: mutable $(name_at_state : usesite) : State ]>
      }
    }
    
    state : HState
    {
      get
      {
        history.Head.History.state
      }
    }
    
    
    public Emit() : void
    {
      def defs = history_field :: clear_func :: save_func :: [];
      gen.Define(defs); // add definition to state class
      foreach (h in history)
        h.Emit()
    }
  }

  /// <summary>
  /// Description of History.
  /// </summary>
  [Record]
  class GenHistory
  {
    gen : StaticGenerator;
    history_paths : list[HistoryPath];
    history       : list[PseudoState.History];

    public Define() : void
    {
      def group = history.GroupBy(x => x.state);
      foreach (hist in group)
      {
        def emit = HistoryEmitter(hist.Map(SingleHistoryEmit(gen, _)), gen);
        emit.Emit()
      }

      def body = history_paths.Map(x => (x.Name, x.transition));
      def paths = gen.GetTransitionFunction(body);
      gen.Define(paths)

    }
  }
}
