using System;
using System.Console;
using Nemerle.Utility;
using System.Runtime.InteropServices;
using Nemerle.Late;

[Record]
class Workbook
{
  public Print(copies : int) : void
  {
    _ = late _wb.Worksheets["Счет"].PrintOut(Copies=copies)
  }
  
  _wb : object;
}

class ExcelWraper : IDisposable
{
  _app : object;
  
  public this()
  {
    System.Threading.Thread.CurrentThread.CurrentCulture = System.Globalization.CultureInfo("en-US");
    _app = Activator.CreateInstance(Type.GetTypeFromProgID("Excel.Application"));
    
    if (_app != null)
      WriteLine("Excel created");
    else
      WriteLine("Excel NOT created");
  }
  
  public Open(path : string) : Workbook
  {
    Workbook(late _app.Workbooks.Open(path))
  }

  
  public Visible : bool
  {
    get { late (_app.Visible) :> bool }
    set { late _app.Visible = value }
  }
  
  public Close() : void
  {
    late _ = _app.Quit()
  }
  
  public Dispose() : void
  {
    def rc = Marshal.ReleaseComObject(_app);
    WriteLine($"Refcount: $rc");
  }
}

module Program
{

  Main() : void
  {
    using (def excel = ExcelWraper())
    {
      excel.Visible = true;
      def wb = excel.Open(@"C:\MyProjects\Tests\N\NReporter\NReporter\ШаблонСчетаНакладнойСчФактуры.xml");
      wb.Print(2);
      //excel.Visible = false;
      excel.Close();
      WriteLine("...");
    }
  }
}