using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Collections.Generic;
using System.Linq;
using System.Diagnostics;

using Nemerle.Xml;
using Nemerle.WUI.Reactive;
using System.Xml.Linq;
using Test;

namespace Nemerle.WUI.Reactive
{
  public module Utils
  {
    public ToJson(value : string) : string
    {
      def sb = Text.StringBuilder(value.Length + 2);
      _ = sb.Append(' ') // this char will replased later by '"'.
      .Append(value).Replace(<#"#>, <#""#>).Replace("\n", <#\n#>)
      .Append('"');
      
      sb[0] = '"';
      
      sb.ToString()
    }
    
    public VMArrayToJson[T](values : VMArray[T]) : string
    {
      $<#[..$(values; ", "; ToJson)]#>
    }

    public ToJson[T](value : T) : string
    {
      match (value)
      {
        | x is string       => ToJson(x)
        | x is IJsonSupport => x.ToJson()
        | x is bool         => if (x) "true" else "false"
        | _                 => value.ToString()
      }      
    }

    public MakeAndShowPage[ViewModel](pagePath  : string, 
                                      viewModel : ViewModel, 
                                      makeView  : ViewModel -> XElement) : void
      where ViewModel: IViewModel
    {
      def path = MakePage(pagePath, viewModel, makeView);
      _ = Process.Start(path);
    }
    
    /// returns page path
    MakePage[ViewModel](pagePath : string, viewModel : ViewModel, makeView : ViewModel -> XElement) : string
      where ViewModel: IViewModel
    {
      def testView = makeView(viewModel);
      def page = Template.Load("PageTemplate.htm");
      
      page.SetContent("MainView", testView);
      
      //TODO: Implement support of CDATA
      def script = XElement.Parse($<#
  <script type='text/javascript'>
    /*<![CDATA[*/
    $(viewModel.GetViewModelScript())
    /*]]>*/
  </script>
      #>);
      page.SetContent("Scripr", script);
      
      page.Save(pagePath, SaveOptions.DisableFormatting);
      pagePath
    }
  }
}
