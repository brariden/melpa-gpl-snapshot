using System;
using SCG = System.Collections.Generic;

using Nemerle.Assertions;
using Nemerle.Utility;
using Nemerle.Collections;
using Nemerle.Compiler;

using PT = Nemerle.Compiler.Parsetree;
using PEG = Nemerle.Peg;
using CS = CSharpParser;

namespace CSharpToNemerle
{
  public partial class AstConverter
  {
    public this(manager : ManagerClass)
    {
      this.manager          = manager;
      this.isLastStatement  = true;
      this.isIntoExpression = false;
      this.labelScope       = null;
      this.switchScope      = null;
      this.isBreakFromLoop  = true;
    }

    private this( manager           : ManagerClass,
                  currentEnv        : GlobalEnv,
                  typeDecls         : SCG.List[PT.TopDeclaration],
                  locationConverter : LocationConverter,
                  isLastStatement   : bool,
                  isIntoExpression  : bool,
                  labelScope        : LabelScope,
                  switchScope       : SwitchScope,
                  isBreakFromLoop   : bool)
    {
      this.manager            = manager;
      this.currentEnv         = currentEnv;
      this.typeDecls          = typeDecls;
      this.locationConverter  = locationConverter;
      this.isLastStatement    = isLastStatement;
      this.isIntoExpression   = isIntoExpression;
      this.labelScope         = labelScope;
      this.switchScope        = switchScope;
      this.isBreakFromLoop    = isBreakFromLoop;
    }

    private WithLastStatement(value : bool) : AstConverter
    {
      if(isLastStatement == value)
        this
      else
        AstConverter(
          manager           = this.manager,
          currentEnv        = this.currentEnv,
          typeDecls         = this.typeDecls,
          locationConverter = this.locationConverter,
          isLastStatement   = value,
          isIntoExpression  = this.isIntoExpression,
          labelScope        = this.labelScope,
          switchScope       = this.switchScope,
          isBreakFromLoop   = this.isBreakFromLoop)
    }

    private WithIntoExpression(value : bool) : AstConverter
    {
      if(isIntoExpression == value)
        this
      else
        AstConverter(
          manager           = this.manager,
          currentEnv        = this.currentEnv,
          typeDecls         = this.typeDecls,
          locationConverter = this.locationConverter,
          isLastStatement   = this.isLastStatement,
          isIntoExpression  = value,
          labelScope        = this.labelScope,
          switchScope       = this.switchScope,
          isBreakFromLoop   = this.isBreakFromLoop)
    }

    private WithNewLabelScope(newLabelScope : LabelScope) : AstConverter
    {
      AstConverter(
        manager             = this.manager,
        currentEnv          = this.currentEnv,
        typeDecls           = this.typeDecls,
        locationConverter = this.locationConverter,
        isLastStatement     = this.isLastStatement,
        isIntoExpression    = this.isIntoExpression,
        labelScope          = newLabelScope,
        switchScope         = SwitchScope(newLabelScope),
        isBreakFromLoop     = this.isBreakFromLoop)
    }

    private WithNewSwitchScope() : AstConverter
    {
      AstConverter(
        manager             = this.manager,
        currentEnv          = this.currentEnv,
        typeDecls           = this.typeDecls,
        locationConverter   = this.locationConverter,
        isLastStatement     = this.isLastStatement,
        isIntoExpression    = this.isIntoExpression,
        labelScope          = this.labelScope,
        switchScope         = SwitchScope(this.labelScope),
        isBreakFromLoop     = false)
    }

    private WithBreakFromLoop(value : bool) : AstConverter
    {
      if(isBreakFromLoop == value)
        this
      else
        AstConverter(
          manager           = this.manager,
          currentEnv        = this.currentEnv,
          typeDecls         = this.typeDecls,
          locationConverter = this.locationConverter,
          isLastStatement   = this.isLastStatement,
          isIntoExpression  = this.isIntoExpression,
          labelScope        = this.labelScope,
          switchScope       = this.switchScope,
          isBreakFromLoop   = value)
    }

    manager : ManagerClass;

    [RecordIgnore] mutable currentEnv : GlobalEnv;

    [RecordIgnore] mutable typeDecls : SCG.List[PT.TopDeclaration];

    [RecordIgnore] mutable locationConverter : LocationConverter;

    isLastStatement : bool;

    isIntoExpression : bool;

    labelScope : LabelScope;

    switchScope : SwitchScope;

    isBreakFromLoop : bool;

    public Convert(
      [NotNull] compilationUnit   : CS.CompilationUnit,
      [NotNull] locationConverter : LocationConverter,
      [NotNull] definedSymbols    : SCG.IEnumerable[string]) : list[PT.TopDeclaration]
    {
      manager.MacroColors.PushUseSiteColor();
      try
      {
        this.typeDecls = SCG.List();
        this.currentEnv = manager.CoreEnv.SetDefines(
        {
          mutable map = Map();
          foreach(symbol in definedSymbols)
            map = map.Add(symbol, true);
          map
        });
        this.locationConverter = locationConverter;
        def context = WithNewLabelScope(LabelScope(this.manager, this.locationConverter));
        context.ConvertExternAliases(compilationUnit.ExternAliases);
        context.ConvertUsingDirectives(compilationUnit.UsingDirectives);
        context.ConvertGlobalAttributes(compilationUnit.GlobalAttributes);
        context.ConvertNamespaceNodes(compilationUnit.Members);

        NList.ToList(typeDecls)
      }
      finally
      {
        manager.MacroColors.PopColor();
      }
    }
  }
}