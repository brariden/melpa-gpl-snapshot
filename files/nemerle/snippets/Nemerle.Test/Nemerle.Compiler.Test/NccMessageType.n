using Nemerle;
using Nemerle.Collections;
using Nemerle.Text;
using Nemerle.Utility;

using System;
using System.Text.RegularExpressions;
using System.Collections.Generic;
using System.Linq;

namespace Nemerle.Compiler.Test
{
  internal variant NccMessageType
  {
    | Unknown
    | Error   { line : int }
    | Warning { line : int }
    | Hint    { line : int }
    | GlobalError
    | GlobalWarning

    public static Parse(text : string) : NccMessageType
    {
      regexp match(text)
      {
        | @".*?:(?<lineNumber : int>\d*):\d*:(\d*:\d*:)?\s+error:.*"   => Error(lineNumber)
        | @".*?:(?<lineNumber : int>\d*):\d*:(\d*:\d*:)?\s+warning:.*" => Warning(lineNumber)
        | @".*?:(?<lineNumber : int>\d*):\d*:(\d*:\d*:)?\s+hint:.*"    => Hint(lineNumber)
        | ".*?error:.*"   => GlobalError()
        | ".*?warning:.*" => GlobalWarning()
        | _               => Unknown()
      }
    }
  }
}
