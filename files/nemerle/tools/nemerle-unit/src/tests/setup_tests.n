using NemerleUnit
using NUnit.Core
using Nemerle.Collections
using Nemerle.IO
using System.Collections

[NUnit.Framework.TestFixture] public class TestSetup
    
    public container_method(): void

        setup
            def fixture_with_setup = TestHelper.GetFixture("WithSetup")
            def tests = TestHelper.GetAllTests(fixture_with_setup)

        test "dll got loaded successfully"
            NUnit.Framework.Assert.IsNotNull(fixture_with_setup,"could not load the assembly")
        
        test "a nunit test defined normally is not broken"
            assert TestHelper.TestPasses("regular_nunit_test", tests)

        test "existance of setup doesn't affect tests that don't use anything from it"
            assert !TestHelper.TestPasses("failing test not using anything from setup",tests)
            assert TestHelper.TestPasses("passing test not using anything from setup", tests)

        test "passing test passes"
            assert TestHelper.TestPasses("passing test", tests)

        test "test that should fail fails"
            assert !TestHelper.TestPasses("failing test", tests)
