using System;
using System.IO;
using System.Diagnostics;
using SCG = System.Collections.Generic;
using Nemerle.Assertions;
using Nemerle.Collections;
using Nemerle.Compiler;
using Nemerle.Compiler.Parsetree;
using Nemerle.Compiler.Utils.Async;
using Nemerle.Imperative;
using Nemerle.Surround;
using Nemerle.Utility;

using Nemerle.Compiler.Utils;
using Typed = Nemerle.Compiler.Typedtree;
using SR = System.Reflection;
using Nemerle.Completion2;

namespace Nemerle.Completion2
{
  internal partial class Engine
  {
    public BeginFindMethodsToOverride(source : IIdeSource, line : int, col : int) : CodeInfoAsyncRequest
    {
      def request = CodeInfoAsyncRequest(AsyncRequestType.FindMethodsToOverride,
        this, source, FindMethodsToOverride, line, col);
      AsyncWorker.AddWork(request);
      request
    }

    private FindMethodsToOverride(request : AsyncRequest) : void
    {
      AsyncWorker.CheckCurrentThreadIsTheAsyncWorker();
      def fileIndex = request.Source.FileIndex;

      surroundwith (currentAsyncRequest)
      try
      {
        if (IsBuildTypesTreeInProgress)
          AsyncWorker.AddWork(request);
        else
        {
          def project = this.Project;

          if (project == null)
          {
            _ = BeginBuildTypesTree();
            AsyncWorker.AddWork(request);
          }
          else
          {
            def req = request :> CodeInfoAsyncRequest;
            match(project.FindMethodsToOverride(fileIndex, req.Line, req.Column))
            {
              | Some((ty, notOverriden)) when notOverriden.Length > 0 =>
                AsyncWorker.AddResponse(() => 
                  _callback.AddOverrideMembers(request.Source, ty, notOverriden : array[_]));

              | _ => AsyncWorker.AddResponse(() => _callback.ShowMessage("No unimplemented methods found.", MessageType.Info))
            }
            request.MarkAsCompleted();
          }
        }
      }
      catch
      { | e is CompilationAbortedException =>
          def msg = $"The GetQuickTipInfo operation aborted at: $(e.Message)";
          throw CompilationAbortedException(msg, e);
      }
    }
  } // end class Engine
} // end namespace
